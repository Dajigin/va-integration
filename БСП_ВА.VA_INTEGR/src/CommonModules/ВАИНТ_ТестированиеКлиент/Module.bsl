#Область СлужебныйПрограмныйИнтерфейс
Процедура СозданиеТестовыхДанных(МассивОбъектов,
		ДополнительныеПараметры) Экспорт

	ИмяФормы = ДополнительныеПараметры.Форма.ИмяФормы;
	РезультатОбновления = ОбновитьСтруктуруКаталоговТестов(ИмяФормы, Истина);
	Если НЕ РезультатОбновления Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементТестирования = ВАИНТ_ТестированиеВызовСервера.ОписаниеЭлементаТестирования(ИмяФормы);
	ИмяОбъекта = ЭлементТестирования.ИмяВладельца;
	ТипОбъекта = ЭлементТестирования.ТипВладельца;
	
	
	ФайлСозданияТестовыхДанных = ВАИНТ_ТестированиеКлиентСервер.ФайлСозданияТестовыхДанных(ТипОбъекта, ИмяОбъекта);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("КаталогПроекта", ВАИНТ_ТестированиеКлиентСервер.БазовыйКаталогТестирования());
	
	ФормаОбработкиТестирования = ОткрытьФорму("Обработка.VanessaAutomationsingle.Форма.УправляемаяФорма", ПараметрыФормы);
	ФормаОбработкиТестирования.Объект.КаталогФич = ФайлСозданияТестовыхДанных;
	ФормаОбработкиТестирования.ЗагрузитьФичи();
КонецПроцедуры

Функция ОбновитьСтруктуруКаталоговТестов(ИмяФормы,
		ПодтверждениеПриСоздании = Ложь) Экспорт
	ЭлементТестирования = ВАИНТ_ТестированиеВызовСервера.ОписаниеЭлементаТестирования(ИмяФормы);
	ИмяЭлемента = ЭлементТестирования.ИмяВладельца;
	ТипОбъекта = ЭлементТестирования.ТипВладельца;
	БазовыйКаталогЭлемента = ВАИНТ_ТестированиеКлиентСервер.КаталогТестовПоТипуОбъекта(ТипОбъекта);
	
	ИмяКаталогаТестовЭлемента = БазовыйКаталогЭлемента + ИмяЭлемента;
	КаталогТестов = Новый Файл(ИмяКаталогаТестовЭлемента);
	Если НЕ КаталогТестов.Существует() Тогда

		Если ПодтверждениеПриСоздании Тогда
			ТекстВопроса = "Создать файлы теста для " + ИмяЭлемента + "?";

			ПоказатьВопрос(Новый ОписаниеОповещения("ПодтверждениеСозданияФайловТестовЗавершение", ЭтотОбъект, ИмяФормы), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			Возврат Ложь;
		КонецЕсли;

		СоздатьКаталог(ИмяКаталогаТестовЭлемента);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Обновление каталогов завершено. Повторите запуск тестов.");
	КонецЕсли;

	ПутьКФайлуШаблона = ВАИНТ_ТестированиеКлиентСервер.ПутьКФайлуШаблонаФичи();

	ФайлСозданияТД = Новый Файл(ВАИНТ_ТестированиеКлиентСервер.ФайлСозданияТестовыхДанных(ТипОбъекта, ИмяЭлемента));

	Если Не ФайлСозданияТД.Существует() Тогда
		КопироватьФайл(ПутьКФайлуШаблона, ФайлСозданияТД.ПолноеИмя);
	КонецЕсли;

	ОбновитьФайлыФорм(ЭлементТестирования, ИмяКаталогаТестовЭлемента);

	Возврат Истина;
КонецФункции

Процедура ПодтверждениеСозданияФайловТестовЗавершение(Результат, ИмяФормы) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОбновитьСтруктуруКаталоговТестов(ИмяФормы);
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура ОбновитьФайлыФорм(ЭлементТестирования, КаталогТестовЭлемента)
	ПутьКФайлуШаблона = ВАИНТ_ТестированиеКлиентСервер.ПутьКФайлуШаблонаФичи();
	ИмяЭлемента = ЭлементТестирования.ИмяВладельца;
	Для Каждого ИмяФормы Из ЭлементТестирования.Формы Цикл
		ИмяФайлаПроверкиПоведения = ВАИНТ_ТестированиеКлиентСервер.ИмяФайлаПроверкиПоведенияФормы(ИмяЭлемента, ИмяФормы);
		КаталогФормы = Новый Файл(КаталогТестовЭлемента);
		Если Не КаталогФормы.Существует() Тогда
			СоздатьКаталог(КаталогФормы.ПолноеИмя);
		КонецЕсли;

		ФайлПроверкиПоведения = Новый Файл(КаталогФормы.ПолноеИмя + "\"
			+ ИмяФайлаПроверкиПоведения);
		Если Не ФайлПроверкиПоведения.Существует() Тогда
			КопироватьФайл(ПутьКФайлуШаблона, ФайлПроверкиПоведения.ПолноеИмя);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
#КонецОбласти
