// @strict-types
#Область ПрограммныйИнтерфейс

// Возвращает указанную настройку тестирования.
// 
// Параметры:
// 	НастройкаТестирования - ПеречислениеСсылка.ВАИНТ_НастройкиТестирования - настройка, значение которой требуется вернуть.
// Возвращаемое значение:
// 	Произвольный - Значение настройки. Если настройка не задана возвращается Неопределено.
Функция ЗначениеНастройкиТестирования(НастройкаТестирования) Экспорт
	Запись = РегистрыСведений.ВАИНТ_НастройкиТестирования.СоздатьМенеджерЗаписи();
	Запись.НастройкаТестирования = НастройкаТестирования;
	Запись.Прочитать();
	Если Запись.Выбран() Тогда
		Возврат Запись.ЗначениеНастройки;
	Иначе
		СообщениеПользователю = "Значение настройки " + НастройкаТестирования + " не задано."; 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеПользователю);
	КонецЕсли;
КонецФункции

// Удалить объекты, созданные при тестировании.
Процедура УдалитьОбъектыСозданныеПриТестировании() Экспорт
	ВАИНТ_ТестированиеСервер.УдалитьОбъектыСозданныеПриТестировании();
КонецПроцедуры
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
// Описание элемента тестирования.
// 
// Параметры:
//  ИмяФормы - Строка - Имя формы
// 
// Возвращаемое значение:
//  Структура - Описание элемента тестирования:
// * ТипВладельца - Строка -
// * ИмяВладельца - Строка -
// * Формы - Массив из Форма -
Функция ОписаниеЭлементаТестирования(ИмяФормы) Экспорт
	Возврат ВАИНТ_ТестированиеСервер.ОписаниеОбъектаПоИмениФормы(ИмяФормы);
КонецФункции

// Параметры подсистемы авто тестирования.
// 
// Возвращаемое значение:
//  Структура - Параметры подсистемы авто тестирования:
// * ЕстьПодсистемаАвтоТестирования - Булево -
// * ЛогироватьОбъектыАвтоТестирования - Булево - указывает, что в данном сеансе будет использована подсистема логирования 
// 												  объектов автотестирования (для менеджера и клиента тестирования);
// * АвтоТестыВыполняются - Булево - указывает, что клиент тестирования запущен для выполнения тестов в режиме логирования 
//									 объектов, созданных/измененных при тестировании.
Функция ПараметрыПодсистемыАвтоТестирования() Экспорт
	Результат = Новый Структура;
	Результат.Вставить("ЕстьПодсистемаАвтоТестирования", Ложь);
	Результат.Вставить("ЛогироватьОбъектыАвтоТестирования", Ложь);
	Результат.Вставить("АвтоТестыВыполняются", Ложь);
	
	ЕстьПодсистемаТестирования = ВАИНТ_ТестированиеСерверПовтИсп.ЕстьПодсистемаАвтоТестирование();
	
	Если ЕстьПодсистемаТестирования Тогда
		ЛогироватьОбъекты = ПараметрыСеанса.ЛогироватьОбъектыАвтоТестирования;
		Результат.ЛогироватьОбъектыАвтоТестирования = ЛогироватьОбъекты;
		Если ЛогироватьОбъекты Тогда
			Результат.АвтоТестыВыполняются = ПараметрыСеанса.АвтоТестыВыполняются; 
		КонецЕсли; 
	КонецЕсли;
	Возврат Результат;
КонецФункции
#КонецОбласти