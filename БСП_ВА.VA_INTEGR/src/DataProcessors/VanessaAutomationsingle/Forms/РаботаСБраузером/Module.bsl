#Область ОписаниеПеременных

&НаКлиенте
Перем Ванесса;

&НаКлиенте
Перем ОкноБраузера;

#КонецОбласти

#Область ЭкспортныеПроцедурыИФункции

// Делает первичную инициализацию модуля.
&НаКлиенте
Функция ИнициализацияФормы(ВладелецФормы) Экспорт
	Ванесса = ВладелецФормы;
КонецФункции

// Ищет расположение браузера в файловой системе
&НаКлиенте
Процедура НайтиБраузерChrome(ПортБраузера, ПапкаПоиска = "%ProgramFiles%") Экспорт
	
	СисИнфо = Новый СистемнаяИнформация;
	Если СисИнфо.ТипПлатформы = ТипПлатформы.Windows_x86 ИЛИ СисИнфо.ТипПлатформы = ТипПлатформы.Windows_x86_64 Тогда
		ProgramFiles = Ванесса.ПолучитьWshShell().ExpandEnvironmentStrings(ПапкаПоиска);
		ИмяФайла = "\Google\Chrome\Application\chrome.exe";
		Файл = Новый Файл(ProgramFiles + ИмяФайла);
		Если Ванесса.ФайлСуществуетКомандаСистемы(Файл.ПолноеИмя) Тогда
			Ванесса.Объект.КомандаЗапускаБраузера = """" + Файл.ПолноеИмя + """"
				+ ?(ЗначениеЗаполнено(ПортБраузера), 
				" --remote-debugging-port=" + Формат(ПортБраузера, "ЧГ="), "");
		ИначеЕсли ПапкаПоиска = "%ProgramFiles%" Тогда
			НайтиБраузерChrome(ПортБраузера, "%ProgramFiles(x86)%");
		КонецЕсли;	 
	КонецЕсли;
	
КонецПроцедуры	

// Активизирует окно браузера
&НаКлиенте
Процедура АктивизироватьОкноБраузера(ПолныйЭкран = Ложь) Экспорт

	ОкноБраузера = ПолучитьОкноБраузера();
	Если ОкноБраузера <> 0 Тогда
		ВнешняяКомпонента().АктивироватьОкно(ОкноБраузера);
		Если ПолныйЭкран = Истина Тогда
			РаспахнутьПолныйЭкран();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Показывает концентрические круги
&НаКлиенте
Функция ВизуализацияНажатияМыши(Параметры = Неопределено) Экспорт

	ДанныеПараметров = Новый Соответствие;
	ДанныеПараметров.Вставить("color", "white");
	ДанныеПараметров.Вставить("duration", 1000);
	ДанныеПараметров.Вставить("radius", 24);
	ДанныеПараметров.Вставить("width", 12);
	ЗаполнитьЗначенияСвойств(ДанныеПараметров, Параметры);

	Скрипт = "{
		|let p = " + Ванесса.ЗаписатьОбъектJSON(ДанныеПараметров) + ";
		|VanessaMouse.showClick(p);
		|}";
	
	Ванесса.ВыполнитьJavaScriptБраузер(Скрипт);
	
КонецФункции

// Показывает анимацию клика в браузере
&НаКлиенте
Процедура АнимацияКликаВБраузере(ЭлементФормы = Неопределено) Экспорт
	Если НЕ Ванесса.Объект.ПодсвечиватьКликМышкиВБраузереVanessaExt Тогда
		Возврат;
	КонецЕсли;	 
	
	ДанныеПараметров = Новый Соответствие;
	ДанныеПараметров.Вставить("color", "green");
	ДанныеПараметров.Вставить("duration", 500);
	ДанныеПараметров.Вставить("radius", 8);
	ДанныеПараметров.Вставить("width", 20);

	Скрипт = "{
		|let p = " + Ванесса.ЗаписатьОбъектJSON(ДанныеПараметров) + ";
		|VanessaMouse.showClick(p);
		|}";
	
	Ванесса.ВыполнитьJavaScriptБраузер(Скрипт);
	Если ЭлементФормы = Неопределено Тогда
		Ванесса.Отладка("" + ТекущаяДата() + ". Анимация клика без элемента формы.");
	Иначе	
		Если ПустаяСтрока(ЭлементФормы.Имя) Тогда
			Ванесса.Отладка("" + ТекущаяДата() + ". Анимация клика. ЭлементФормы=" + ЭлементФормы.ТекстЗаголовка);
		Иначе	
			Ванесса.Отладка("" + ТекущаяДата() + ". Анимация клика. ЭлементФормы=" + ЭлементФормы.Имя);
		КонецЕсли;	 
	КонецЕсли;	 
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция РаспахнутьПолныйЭкран()

	Скрипт = "{
		|let node = document.createElement('div');
		|node.style.position = 'absolute';
		|node.style.height = '100%';
		|node.style.width = '100%';
		|node.style.overflow = 'hidden';
		|node.style.zIndex = 999;
		|node.addEventListener('click', () => 
		|	document.documentElement.requestFullscreen()
		|);
		|document.body.appendChild(node);
		|window.VanessaFullScreen = node;
		|}";
	Ванесса.ВыполнитьJavaScriptБраузер(Скрипт);
	ВнешняяКомпонента().Пауза(500);

	ТекстJSON = ВнешняяКомпонента().ПолучитьРазмерОкна(ОкноБраузера);
	РазмерОкна = Ванесса.ПрочитатьОбъектJSON(ТекстJSON);
	X = Окр((РазмерОкна.Left + РазмерОкна.Right) / 2);
	Y = Окр((РазмерОкна.Top + РазмерОкна.Bottom) / 2);
	ВнешняяКомпонента().УстановитьПозициюКурсора(X , Y);
	ВнешняяКомпонента().ЭмуляцияНажатияМыши(0);
	ВнешняяКомпонента().Пауза(500);
	
	Скрипт = "{
		|window.VanessaFullScreen.remove();
		|delete window.VanessaFullScreen;
		|}";
	Ванесса.ВыполнитьJavaScriptБраузер(Скрипт);

КонецФункции

&НаКлиенте
Функция ВнешняяКомпонента()
	Возврат Ванесса.ВнешняяКомпонентаДляСкриншотов;
КонецФункции

&НаКлиенте
Функция ПолучитьОкноБраузера()

	СтрокаИдентификатор = Строка(Новый УникальныйИдентификатор);
	Скрипт = "{
		|window.VanessaDocumentTitle = document.title; 
		|document.title = '" + СтрокаИдентификатор + "';
		|}";
	Ванесса.ВыполнитьJavaScriptБраузер(Скрипт);

	ВнешняяКомпонента().Пауза(200);
	ТекстJSON = ВнешняяКомпонента().ПолучитьСписокОкон(0);

	Скрипт = "{
		|document.title = window.VanessaDocumentTitle; 
		|delete window.VanessaDocumentTitle;
		|}";
	Ванесса.ВыполнитьJavaScriptБраузер(Скрипт);

	МассивОкон = Ванесса.ПрочитатьОбъектJSON(ТекстJSON);
	Для Каждого Стр Из МассивОкон Цикл
		ЗаголовокОкна = Неопределено;
		Если Стр.Свойство("Title", ЗаголовокОкна) Тогда
			Если Найти(ЗаголовокОкна, СтрокаИдентификатор) > 0 Тогда  
				ОкноБраузера = Стр.Window;
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Возврат ОкноБраузера;

КонецФункции

#КонецОбласти
