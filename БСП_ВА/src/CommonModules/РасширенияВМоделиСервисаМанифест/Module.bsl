#Область СлужебныйПрограммныйИнтерфейс

// Заполняет переданный объект данными, считываемыми из манифеста расширения.
//
// Параметры:
//  Манифест - ОбъектXDTO - ОбъектXDTO {http://www.1c.ru/1cFresh/ConfigurationExtensions/Manifest/a.b.c.d}ConfigurationExtensionManifest -
//    манифест расширения,
//  ОбъектРасширения - СправочникОбъект.ПоставляемыеРасширения - значения свойств которого будет установлены
//    значениями свойств расширения из манифеста (предположительно СправочникОбъект.ПоставляемыеРасширения),
//  ИмяФайла - Строка - возвращаемая строка, содержит оригинальное имя файла расширения.
//
Процедура ПрочитатьМанифест(Знач Манифест, Знач ОбъектРасширения) Экспорт
	
	ОбъектРасширения.Наименование = Манифест.Name;
	ОбъектРасширения.Имя = Манифест.ObjectName;
	ОбъектРасширения.Версия = Манифест.Version;
	ОбъектРасширения.Информация = Манифест.Description;
	
	ОбъектРасширения.Разрешения.Очистить();
	
	Если Манифест.Permissions <> Неопределено Тогда
		
		Для Каждого Permission Из Манифест.Permissions Цикл
			
			ТипXDTO = Permission.Тип();
			
			Разрешение = ОбъектРасширения.Разрешения.Добавить();
			Разрешение.ВидРазрешения = ТипXDTO.Имя;
			
			Параметры = Новый Структура();
			
			Для Каждого СвойствоXDTO Из ТипXDTO.Свойства Цикл
				
				Контейнер = Permission.ПолучитьXDTO(СвойствоXDTO.Имя);
				
				Если Контейнер <> Неопределено Тогда
					
					Параметры.Вставить(СвойствоXDTO.Имя, Контейнер.Значение);
					
				Иначе
					
					Параметры.Вставить(СвойствоXDTO.Имя);
					
				КонецЕсли;
				
			КонецЦикла;
			
			Разрешение.Параметры = Новый ХранилищеЗначения(Параметры);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрочитатьМанифестJSON(Знач Манифест, Знач ОбъектРасширения) Экспорт 
	
	ЗаполнитьЗначенияСвойств(ОбъектРасширения, Манифест);	
	
	ОбъектРасширения.Разрешения.Очистить();
	
	Для Каждого Разрешение Из Манифест.Разрешения Цикл
		
		НовоеРазрешение = ОбъектРасширения.Разрешения.Добавить();
		НовоеРазрешение.ВидРазрешения = Разрешение.Описание; 
		НовоеРазрешение.Параметры = Новый ХранилищеЗначения(Разрешение.Параметры);

	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти