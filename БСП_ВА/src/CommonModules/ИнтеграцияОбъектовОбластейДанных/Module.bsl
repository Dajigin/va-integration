
#Область ПрограммныйИнтерфейс

#Область ЛогическоеХранилище
    
// МЕТОД НЕ ПОДДЕРЖИВАЕТСЯ.
// Возвращает описание данных логического хранилища.
//
// Параметры:
//  ИдентификаторХранилища - Строка - идентификатор логического хранилища.
//  ИдентификаторДанных    - Строка - идентификатор данных хранилища.
// 
// Возвращаемое значение:
//  Неопределено - Вызывается исключение.
//
Функция Описание(ИдентификаторХранилища, ИдентификаторДанных) Экспорт
	
	ВызватьИсключение ИнтеграцияОбъектовОбластейДанныхСловарь.МетодНеПоддерживается();
	
КонецФункции

// МЕТОД НЕ ПОДДЕРЖИВАЕТСЯ.
// Возвращает данные логического хранилища.
//
// Параметры:
//  ОписаниеДанных - Структура - описание данных хранилища.
// 
// Возвращаемое значение:
//  Неопределено - Вызывается исключение.
//
Функция Данные(ОписаниеДанных) Экспорт
   
	ВызватьИсключение ИнтеграцияОбъектовОбластейДанныхСловарь.МетодНеПоддерживается();
	
КонецФункции

// Записывает данные в логическое хранилище.
// 
// Параметры:
//  ОписаниеДанных - Структура - описание данных хранилища:
//    * ИмяФайла - Строка - имя файла.
//    * Размер - Число - размер файла в байтах.
//    * Данные - ДвоичныеДанные, Строка - двоичные данные файла или расположение файла на диске.
// 
// Возвращаемое значение:
//   Строка - идентификатор данных хранилища.
//   
Функция Загрузить(ОписаниеДанных) Экспорт
    
    Словарь = ИнтеграцияОбъектовОбластейДанныхСловарь;
    УстановитьПривилегированныйРежим(Истина);
    Ответ = ОтветПоУмолчанию();
    Команда = НРег(ОписаниеДанных.ИмяФайла);
    УчетнаяСистема = АвторизованнаяУчетнаяСистема();
    
    Если Команда = Словарь.КомандаУстановить() Тогда
        Попытка
            УстановитьНастройки(УчетнаяСистема, ОписаниеДанных.Данные, Ответ);
        Исключение
            УстановитьВнутреннююОшибку(Ответ, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
        КонецПопытки;
        Возврат Ответ;
    КонецЕсли; 
    
    Если УчетнаяСистема = Неопределено Тогда
        УстановитьОтсутствиеДанных(Ответ);
        Возврат Ответ;
    КонецЕсли;
    
    Если Команда = Словарь.КомандаВыполнить() Тогда
        ПараметрыКоманды = Новый ДвоичныеДанные(ОписаниеДанных.Данные);
        Если ЗначениеЗаполнено(ПараметрыКоманды) Тогда
            ИдентификаторПараметров = ФайлыОбластейДанных.ЗагрузитьФайл(ОписаниеДанных.ИмяФайла + ".settings", ПараметрыКоманды);
        Иначе
            УстановитьОшибкуДанных(Ответ, Словарь.ДанныеОперацииНеЗаданы()); 
            Возврат Ответ;
        КонецЕсли; 
    	Задание = ДобавитьЗаданиеВыполненияКоманды(УчетнаяСистема, ПараметрыКоманды, ИдентификаторПараметров);
        Ответ = СвойстваЗадания(Задание);
    ИначеЕсли Команда = Словарь.КомандаПолучить() Тогда
        // Проверим, есть ли какие либо данные для формирования выгрузки.
        Если РегистрыСведений.ОбъектыКОтправке.Количество(УчетнаяСистема) = 0 Тогда
            УстановитьОтсутствиеДанных(Ответ);
            Возврат Ответ;
        КонецЕсли;
        // Формирование выгрузки выполняется асинхронно, эксклюзивно.
    	Задание = ДобавитьЗаданиеПодготовкиДанных(УчетнаяСистема);
        Ответ = СвойстваЗадания(Задание);
    ИначеЕсли Команда = Словарь.КомандаПодтвердить() Тогда
        // Обработка подтверждения выполняется синхронно.
        СтрокаДанных = ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(ОписаниеДанных.Данные));
        Если Не ЗначениеЗаполнено(СтрокаДанных) Тогда
            УстановитьОшибкуДанных(Ответ, Словарь.ПустоеПодтверждение());
            Возврат Ответ;
        КонецЕсли;
        Попытка
            ДанныеПодтверждений = РаботаВМоделиСервисаБТС.СтруктураИзСтрокиJSON(СтрокаДанных);
        Исключение
            УстановитьОшибкуДанных(Ответ, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
                Словарь.ОшибкаРазбораПодтверждения(), КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
            Возврат Ответ;
        КонецПопытки;
        Попытка
            ОбработатьПодтверждения(УчетнаяСистема, ДанныеПодтверждений);
            УдалитьПустойРезультат(Ответ);
        Исключение
            УстановитьВнутреннююОшибку(Ответ, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
        КонецПопытки;
    ИначеЕсли Команда = Словарь.КомандаОтправить() Тогда
        Если ОписаниеДанных.Данные = Тип("Строка") Тогда
            ПолноеИмя = Неопределено;
            Данные = ОписаниеДанных.Данные;
        Иначе
            ПолноеИмя = ОписаниеДанных.Данные;
            Данные = Неопределено;
    	КонецЕсли;
        ИдентификаторФайла = ФайлыОбластейДанных.ЗагрузитьФайл(ОписаниеДанных.ИмяФайла, Данные, ПолноеИмя);
    	Задание = ДобавитьЗаданиеОбработкиДанных(УчетнаяСистема, Строка(ИдентификаторФайла));
        Ответ = СвойстваЗадания(Задание);
    КонецЕсли; 
    
    Возврат Ответ;
    
КонецФункции

// Возвращает идентификатор хранилища в виде строки.
// 
// Возвращаемое значение:
//   - Строка - идентификатор хранилища. 
//
Функция ИдентификаторХранилища() Экспорт
	
	Возврат "integration";
	
КонецФункции

#КонецОбласти

// Добавляет объект к отправке во внешнюю учетную систему.
//
// Параметры:
//  УчетнаяСистема - ОпределяемыйТип.УчетныеСистемыИнтеграцииОбластейДанных - учетная система.
//  ИдентификаторОбъекта - Строка - идентификатор объекта, должен соответствовать 
//                         требованиям к именованию файлов операционной системы (длина - 50).  
//  Обработчик - Строка - идентификатор обработчика объекта (длина - 50).
//  ДанныеОбъекта - ДвоичныеДанные - данные объекта к отправке (если не указано, данные будут запрошены перед отправкой).
//
Процедура ДобавитьОбъектКОтправке(УчетнаяСистема, ИдентификаторОбъекта, Обработчик, ДанныеОбъекта = Неопределено) Экспорт
    
    ПривилегированныйРежимПриВызове = ПривилегированныйРежим();
    Если Не ПривилегированныйРежимПриВызове Тогда
        УстановитьОтключениеБезопасногоРежима(Истина);
        УстановитьПривилегированныйРежим(Истина);
    КонецЕсли;
    
    ИдентификаторФайла = Неопределено;
    Если ЗначениеЗаполнено(ДанныеОбъекта) Тогда
        ИдентификаторФайла = ФайлыОбластейДанных.ЗагрузитьФайл(ИдентификаторОбъекта, ДанныеОбъекта);
    КонецЕсли; 
    
    ЗарегистрироватьОтправку(УчетнаяСистема, ИдентификаторОбъекта, Обработчик, ИдентификаторФайла);
    
    Если Не ПривилегированныйРежимПриВызове Тогда
    	УстановитьПривилегированныйРежим(Ложь);
    	УстановитьОтключениеБезопасногоРежима(Ложь);
    КонецЕсли;
    
КонецПроцедуры

// Удаляет объект из объектов к отправке в учетную систему.
//
// Параметры:
//  УчетнаяСистема - ОпределяемыйТип.УчетныеСистемыИнтеграцииОбластейДанных - учетная система.
//  ИдентификаторОбъекта - Строка - идентификатор объекта (длина - 50).
//  Обработчик - Строка - идентификатор обработчика объекта (длина - 50).
//
Процедура УдалитьОбъектКОтправке(УчетнаяСистема, ИдентификаторОбъекта = Неопределено, Обработчик = Неопределено) Экспорт
    
    ПривилегированныйРежимПриВызове = ПривилегированныйРежим();
    Если Не ПривилегированныйРежимПриВызове Тогда
        УстановитьОтключениеБезопасногоРежима(Истина);
        УстановитьПривилегированныйРежим(Истина);
    КонецЕсли;
    
    Отбор = Новый Структура;
    Отбор.Вставить("УчетнаяСистема", УчетнаяСистема);
    Отбор.Вставить("ИдентификаторОбъекта", ИдентификаторОбъекта);
    Отбор.Вставить("Обработчик", Обработчик);
    
    РегистрыСведений.ОбъектыКОтправке.УдалитьЗаписи(Отбор);
	
    Если Не ПривилегированныйРежимПриВызове Тогда
    	УстановитьПривилегированныйРежим(Ложь);
    	УстановитьОтключениеБезопасногоРежима(Ложь);
    КонецЕсли;
    
КонецПроцедуры

// Выполняет оповещение внешней учетной системы в соответствие с настройками оповещения.
//
// Параметры:
//  УчетнаяСистема - ОпределяемыйТип.УчетныеСистемыИнтеграцииОбластейДанных - учетная система.
//  ИдентификаторОбъекта - Строка - идентификатор объекта (длина - 50).
//  ВызыватьИсключение - Булево - признак вызова исключения при неудачной отправке оповещения.
//
Процедура ОповеститьОбИзмененииОбъекта(УчетнаяСистема, ИдентификаторОбъекта, ВызыватьИсключение = Ложь) Экспорт
    
    ПривилегированныйРежимПриВызове = ПривилегированныйРежим();
    Если Не ПривилегированныйРежимПриВызове Тогда
        УстановитьОтключениеБезопасногоРежима(Истина);
        УстановитьПривилегированныйРежим(Истина);
    КонецЕсли;
    
    Словарь = ИнтеграцияОбъектовОбластейДанныхСловарь;
    Попытка
        Настройки = Настройки(УчетнаяСистема);
        Если Не Настройки.ОповещатьОбИзменениях Тогда
            ВызватьИсключение Словарь.ОповещенияОбИзмененияхНеИспользуются();
        КонецЕсли; 
        Запрос = Новый Запрос;
        Запрос.Текст = 
        	"ВЫБРАТЬ ПЕРВЫЕ 1
            |   ОбъектыКОтправке.ИдентификаторОбъекта КАК ИдентификаторОбъекта,
            |   ОбъектыКОтправке.Обработчик КАК Обработчик,
            |   ФайлыОбластейДанных.Идентификатор КАК ИдентификаторФайла,
            |   ФайлыОбластейДанных.ФайлCRC32 КАК Версия
            |ИЗ
            |   РегистрСведений.ОбъектыКОтправке КАК ОбъектыКОтправке
            |       ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ФайлыОбластейДанных КАК ФайлыОбластейДанных
            |       ПО ОбъектыКОтправке.ИдентификаторФайла = ФайлыОбластейДанных.Идентификатор
            |           И (НЕ ФайлыОбластейДанных.Временный)
            |ГДЕ
            |   ОбъектыКОтправке.УчетнаяСистема = &УчетнаяСистема
            |   И ОбъектыКОтправке.ИдентификаторОбъекта = &ИдентификаторОбъекта";
        
        Запрос.УстановитьПараметр("УчетнаяСистема", УчетнаяСистема);
        Запрос.УстановитьПараметр("ИдентификаторОбъекта", ИдентификаторОбъекта);
        Результат = Запрос.Выполнить();
        Если Результат.Пустой() Тогда
            ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
                Словарь.ДанныеОбъектаНеУстановлены(), ИдентификаторОбъекта);
        КонецЕсли;
        Выборка = Результат.Выбрать();
        Выборка.Следующий();
        АдресОбъекта = СтрШаблон("%1/hs/dt/%2/%3/%4",
            ПолучитьНавигационнуюСсылкуИнформационнойБазы(),
            Словарь.ПолеХранилище(),
            ФайлыОбластейДанных.ИдентификаторХранилища(),
            Строка(Выборка.ИдентификаторФайла));
        СвойстваСообщения = Новый Структура;
        СвойстваСообщения.Вставить("url", АдресОбъекта);
        СвойстваСообщения.Вставить("tenant", РаботаВМоделиСервиса.ЗначениеРазделителяСеанса());
        СвойстваСообщения.Вставить(Словарь.ПолеХранилище(), ФайлыОбластейДанных.ИдентификаторХранилища());
        СвойстваСообщения.Вставить(Словарь.ПолеИдентификатор(), Строка(Выборка.ИдентификаторФайла));
        СвойстваОбъекта = СвойстваОбъекта(Выборка.ИдентификаторОбъекта, Выборка.Обработчик, Выборка.Версия);
        ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(СвойстваСообщения, СвойстваОбъекта); 
        Если Настройки.ПодписыватьДанные Тогда
            ПодписатьДанные(СвойстваСообщения, ПолучитьДвоичныеДанныеИзСтроки(Настройки.КлючПодписи));
        КонецЕсли;
        Данные = РаботаВМоделиСервисаБТС.СтрокаИзСтруктурыJSON(СвойстваСообщения);
        ОтправитьОповещение(Данные, Настройки);
        
    Исключение
        ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
        ЗаписьЖурналаРегистрации(Словарь.КорневоеСобытие() + "." + Словарь.ОповещениеОбИзменении(),
            УровеньЖурналаРегистрации.Предупреждение, УчетнаяСистема.Метаданные(), УчетнаяСистема, 
            СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Словарь.НеУдалосьОтправитьОповещение(), ОписаниеОшибки));
        Если ВызыватьИсключение Тогда
            ВызватьИсключение ОписаниеОшибки;
        КонецЕсли; 
    КонецПопытки;	
	
    Если Не ПривилегированныйРежимПриВызове Тогда
    	УстановитьПривилегированныйРежим(Ложь);
    	УстановитьОтключениеБезопасногоРежима(Ложь);
    КонецЕсли;
    
КонецПроцедуры

// Возвращает настройки внешней учетной системы.
//
// Параметры:
//  УчетнаяСистема - ОпределяемыйТип.УчетныеСистемыИнтеграцииОбластейДанных - учетная система.
//  КлючиНастроек - Строка, Массив - ключи настроек, по которым нужно вернуть значения.
//
// Возвращаемое значение:
//  Структура - Настройки учетной системы:
//    * ОповещатьОбИзменениях - Булево - признак использования оповещений при создании/изменении данных в приложении.
//    * АдресСервиса - Строка - адрес сервиса приема оповещений об изменениях.
//    * СпособАутентификации - ПеречислениеСсылка.СпособыАутентификации - способ аутентификации в сервисе приема оповещений.
//    * Логин - Строка - логин аутентификации в сервисе приема оповещений (используется при basic-аутентификации).
//    * Пароль - Строка - пароль аутентификации в сервисе приема оповещений (используется при basic-аутентификации).
//    * ИспользоватьСертификат - Булево - признак использования сертификата при установке соединения с сервисом приема оповещений.
//    * ИмяСертификата - Строка - имя файла сертификата.
//    * ПарольСертификата - Строка -  пароль сертификата (используется, если задано свойство ИспользоватьСертификат).
//    * ДанныеСертификата - ДвоичныеДанные - двоичные данные сертификата в base64 (используется, если задано свойство ИспользоватьСертификат).
//    * ПодписыватьДанные - Булево - признак использования подписи данных при отправке их в сервис приема оповещений.
//    * КлючПодписи - Строка - секретное слово, для подписи отправляемых данных. Подпись выполняется с помощью алгоритма HMACSHA256.
//
Функция Настройки(УчетнаяСистема, Знач КлючиНастроек = Неопределено) Экспорт
    
    ПривилегированныйРежимПриВызове = ПривилегированныйРежим();
    Если Не ПривилегированныйРежимПриВызове Тогда
        УстановитьОтключениеБезопасногоРежима(Истина);
        УстановитьПривилегированныйРежим(Истина);
    КонецЕсли;
    
    Если КлючиНастроек = Неопределено Тогда
        ЗаполняемыеНастройки = ЗаполняемыеНастройки();
    Иначе
        Если ТипЗнч(КлючиНастроек) = Тип("Строка") Тогда
            КлючиНастроек = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
                КлючиНастроек, ",", Истина, Истина);
        КонецЕсли; 
        ЗаполняемыеНастройки = ЗаполняемыеНастройки(КлючиНастроек);
    КонецЕсли; 
    Настройки = Новый Структура;
    ПоляУчетнойСистемы = Новый Массив;
    МетаданныеУчетнойСистемы = УчетнаяСистема.Метаданные();
    Отбор = Новый Структура("БезопасноеХранение", Ложь);
    Для Каждого Настройка Из ЗаполняемыеНастройки.НайтиСтроки(Отбор) Цикл
        Настройки.Вставить(Настройка.ИмяСвойства, Настройка.Тип.ПривестиЗначение(Неопределено));
        Если МетаданныеУчетнойСистемы.Реквизиты.Найти(Настройка.ИмяСвойства) <> Неопределено Тогда
            ПоляУчетнойСистемы.Добавить(Настройка.ИмяСвойства);    
        КонецЕсли; 
    КонецЦикла;
    МенеджерНастроек = РегистрыСведений.НастройкиУчетныхСистем.СоздатьМенеджерЗаписи();
    МенеджерНастроек.УчетнаяСистема = УчетнаяСистема;
    МенеджерНастроек.Прочитать();
    Если МенеджерНастроек.Выбран() Тогда
        ЗаполнитьЗначенияСвойств(Настройки, МенеджерНастроек);
    КонецЕсли; 
    
    Отбор = Новый Структура("БезопасноеХранение", Истина);
    
    Для Каждого Настройка Из ЗаполняемыеНастройки.НайтиСтроки(Отбор) Цикл
        Настройки.Вставить(Настройка.ИмяСвойства, 
            ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(УчетнаяСистема, Настройка.ИмяСвойства)); 
    КонецЦикла;
    
    Если Не ПривилегированныйРежимПриВызове Тогда
    	УстановитьПривилегированныйРежим(Ложь);
    	УстановитьОтключениеБезопасногоРежима(Ложь);
    КонецЕсли;
    
    Возврат Настройки;
    
КонецФункции

// Возвращает шаблон для помещения результатов выполнения команды
// 
// Возвращаемое значение:
//  ТаблицаЗначений - шаблон выполнения команды для возврата результатов:
//    * ИдентификаторОбъекта - Строка - идентификатор объекта.
//    * Обработчик - Строка - идентификатор обработчика.
//
Функция НовыйРезультатыВыполненияКоманды() Экспорт
	
	Результаты = Новый ТаблицаЗначений;
    Результаты.Колонки.Добавить("ИдентификаторОбъекта", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	Результаты.Колонки.Добавить("Обработчик", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	Возврат Результаты;
	
КонецФункции

#КонецОбласти 

#Область СлужебныйПрограммныйИнтерфейс

// Обработчик задания очереди заданий.
//
// Параметры:
//  УчетнаяСистема - ОпределяемыйТип.УчетныеСистемыИнтеграцииОбластейДанных - учетная система.
//  ИдентификаторПараметров - Строка - идентификатор файла параметров выполнения команды (длинна 36).
//
Процедура ВыполнитьКоманду(УчетнаяСистема, ИдентификаторПараметров) Экспорт
	
	Словарь = ИнтеграцияОбъектовОбластейДанныхСловарь;
    СловарьОчередь = ОчередьЗаданийВнешнийИнтерфейсСловарь;
    
    Параметры = ФайлыОбластейДанных.ДвоичныеДанныеФайла(ИдентификаторПараметров);
    КлючЗадания = КлючЗаданияВыполненияКоманды(УчетнаяСистема, Параметры);
    ИдентификаторЗадания = ОчередьЗаданийВнешнийИнтерфейс.ИдентификаторЗадания(КлючЗадания);
    ПотокДанных = Параметры.ОткрытьПотокДляЧтения();
    
    Попытка
        МенеджерУчетнойСистемы = ОбщегоНазначения.МенеджерОбъектаПоСсылке(УчетнаяСистема);
        КодВозвратаОбработчика = СловарьОчередь.КодСостоянияВыполнено();
        ОшибкаОбработчика = Ложь;
        СообщениеОбОшибкеОбработчика = "";
                
        Результат = МенеджерУчетнойСистемы.ВыполнитьКоманду(
            УчетнаяСистема, 
            ПотокДанных, 
            КодВозвратаОбработчика, 
            ОшибкаОбработчика, 
            СообщениеОбОшибкеОбработчика);
            
        СвойстваЗадания = РегистрыСведений.СвойстваЗаданий.НовыйСвойстваЗадания();
        СвойстваЗадания.КодСостояния = КодВозвратаОбработчика;
        СвойстваЗадания.Ошибка = ОшибкаОбработчика;
        СвойстваЗадания.СообщениеОбОшибке = СообщениеОбОшибкеОбработчика;
        
        ШаблонРезультата = НовыйРезультатыВыполненияКоманды();
        Если ЗначениеЗаполнено(Результат) И ТипЗнч(Результат) <> ТипЗнч(ШаблонРезультата) Тогда
            ВызватьИсключение СтрШаблон(Словарь.ПереданРезультатНекорректногоТипа(), ТипЗнч(ШаблонРезультата), ТипЗнч(Результат));
        КонецЕсли;
        
        Если ЗначениеЗаполнено(Результат) 
           И Результат.Колонки.Найти("ИдентификаторОбъекта") <> Неопределено 
           И Результат.Колонки.Найти("Обработчик") <> Неопределено Тогда
           
            Запрос = Новый Запрос;
            Запрос.Текст = 
            	"ВЫБРАТЬ
                |   РезультатыВыполненияКоманды.ИдентификаторОбъекта КАК ИдентификаторОбъекта,
                |   РезультатыВыполненияКоманды.Обработчик КАК Обработчик
                |ПОМЕСТИТЬ РезультатыВыполненияКоманды
                |ИЗ
                |   &РезультатыВыполненияКоманды КАК РезультатыВыполненияКоманды
                |;
                |
                |////////////////////////////////////////////////////////////////////////////////
                |ВЫБРАТЬ
                |   ОбъектыКОтправке.ИдентификаторОбъекта КАК ИдентификаторОбъекта,
                |   ОбъектыКОтправке.Обработчик КАК Обработчик,
                |   ФайлыОбластейДанных.Идентификатор КАК ИдентификаторФайла
                |ИЗ
                |   РегистрСведений.ОбъектыКОтправке КАК ОбъектыКОтправке
                |       ВНУТРЕННЕЕ СОЕДИНЕНИЕ РезультатыВыполненияКоманды КАК РезультатыВыполненияКоманды
                |       ПО ОбъектыКОтправке.ИдентификаторОбъекта = РезультатыВыполненияКоманды.ИдентификаторОбъекта
                |           И ОбъектыКОтправке.Обработчик = РезультатыВыполненияКоманды.Обработчик
                |       ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФайлыОбластейДанных КАК ФайлыОбластейДанных
                |       ПО ОбъектыКОтправке.ИдентификаторФайла = ФайлыОбластейДанных.Идентификатор
                |           И (НЕ ФайлыОбластейДанных.Временный)
                |       ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияИнтеграцииОбъектов КАК СостоянияИнтеграцииОбъектов
                |       ПО ОбъектыКОтправке.УчетнаяСистема = СостоянияИнтеграцииОбъектов.УчетнаяСистема
                |           И ОбъектыКОтправке.ИдентификаторОбъекта = СостоянияИнтеграцииОбъектов.ИдентификаторОбъекта
                |ГДЕ
                |   ОбъектыКОтправке.УчетнаяСистема = &УчетнаяСистема
                |   И ЕСТЬNULL(СостоянияИнтеграцииОбъектов.Состояние, &ПустоеСостояние) <> &Ошибка";
            Запрос.УстановитьПараметр("УчетнаяСистема", УчетнаяСистема);
            Запрос.УстановитьПараметр("Ошибка", Перечисления.СостоянияИнтеграцииОбъектов.Ошибка);
            Запрос.УстановитьПараметр("ПустоеСостояние", Перечисления.СостоянияИнтеграцииОбъектов.ПустаяСсылка());
            Запрос.УстановитьПараметр("РезультатыВыполненияКоманды", Результат);
            РезультатЗапроса = Запрос.Выполнить();
            Если Не РезультатЗапроса.Пустой() Тогда
                Выборка = РезультатЗапроса.Выбрать();
                Архиватор = Новый ЗаписьZipФайла;
                КаталогФайлов = ПолучитьИмяВременногоФайла();
                ДанныеМанифеста = Новый Массив;
                Пока Выборка.Следующий() Цикл
                    ДобавитьОбъектКВыгрузке(УчетнаяСистема, КаталогФайлов, Архиватор, ДанныеМанифеста, Выборка, Выборка.ИдентификаторФайла);
                КонецЦикла;
                Манифест = Новый Структура(Словарь.ИмяПоляСоставаДанных(), ДанныеМанифеста);
                Архиватор.Добавить(ЗаписатьМанифест(Манифест, КаталогФайлов));
                ДвоичныеДанныеАрхива = Архиватор.ПолучитьДвоичныеДанные(,, МетодСжатияZIP.Сжатие, УровеньСжатияZIP.Максимальный);
                ИмяАрхива = Строка(ИдентификаторЗадания) + ".zip";
                ИдентификаторФайлаАрхива = ФайлыОбластейДанных.ЗагрузитьФайл(ИмяАрхива, ДвоичныеДанныеАрхива,, Истина);
                Попытка
                    УдалитьФайлы(КаталогФайлов);
        		Исключение
        			Комментарий = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
        			ЗаписьЖурналаРегистрации(Словарь.КорневоеСобытие(), УровеньЖурналаРегистрации.Ошибка, , , Комментарий);
                КонецПопытки;
                РезультатЗадания = Новый Структура;
                РезультатЗадания.Вставить(Словарь.ПолеХранилище(), ФайлыОбластейДанных.ИдентификаторХранилища());
                РезультатЗадания.Вставить(Словарь.ПолеИдентификатор(), Строка(ИдентификаторФайлаАрхива));
                СвойстваЗадания.Результат = РаботаВМоделиСервисаБТС.СтрокаИзСтруктурыJSON(РезультатЗадания);
            КонецЕсли; 
            ФайлыОбластейДанных.УстановитьПризнакВременного(ИдентификаторПараметров);
        КонецЕсли; 
        РегистрыСведений.СвойстваЗаданий.Установить(ИдентификаторЗадания, СвойстваЗадания);
    Исключение
        ОчередьЗаданийВнешнийИнтерфейс.УстановитьВнутреннююОшибку(
            ИдентификаторЗадания, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
        ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
    КонецПопытки
	
КонецПроцедуры
 
// Обработчик задания очереди заданий.
//
// Параметры:
//  УчетнаяСистема - ОпределяемыйТип.УчетныеСистемыИнтеграцииОбластейДанных - учетная система
//                   по которой готовятся данные.
//
Процедура ПодготовитьДанные(УчетнаяСистема) Экспорт
    
	Словарь = ИнтеграцияОбъектовОбластейДанныхСловарь;
    СловарьОчередь = ОчередьЗаданийВнешнийИнтерфейсСловарь;
    КлючЗадания = КлючЗаданияПодготовкиДанных(УчетнаяСистема);
    ИдентификаторЗадания = ОчередьЗаданийВнешнийИнтерфейс.ИдентификаторЗадания(КлючЗадания);
    Запрос = Новый Запрос;
    Запрос.Текст = 
    	"ВЫБРАТЬ
        |   ОбъектыКОтправке.ИдентификаторОбъекта КАК ИдентификаторОбъекта,
        |   ОбъектыКОтправке.Обработчик КАК Обработчик,
        |   ФайлыОбластейДанных.Идентификатор КАК ИдентификаторФайла
        |ИЗ
        |   РегистрСведений.ОбъектыКОтправке КАК ОбъектыКОтправке
        |       ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФайлыОбластейДанных КАК ФайлыОбластейДанных
        |       ПО ОбъектыКОтправке.ИдентификаторФайла = ФайлыОбластейДанных.Идентификатор
        |           И (НЕ ФайлыОбластейДанных.Временный)
        |       ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияИнтеграцииОбъектов КАК СостоянияИнтеграцииОбъектов
        |       ПО ОбъектыКОтправке.УчетнаяСистема = СостоянияИнтеграцииОбъектов.УчетнаяСистема
        |           И ОбъектыКОтправке.ИдентификаторОбъекта = СостоянияИнтеграцииОбъектов.ИдентификаторОбъекта
        |ГДЕ
        |   ОбъектыКОтправке.УчетнаяСистема = &УчетнаяСистема
        |   И ЕСТЬNULL(СостоянияИнтеграцииОбъектов.Состояние, &ПустоеСостояние) <> &Ошибка";
    
    Запрос.УстановитьПараметр("УчетнаяСистема", УчетнаяСистема);
    Запрос.УстановитьПараметр("Ошибка", Перечисления.СостоянияИнтеграцииОбъектов.Ошибка);
    Запрос.УстановитьПараметр("ПустоеСостояние", Перечисления.СостоянияИнтеграцииОбъектов.ПустаяСсылка());
    
    Попытка
        РезультатЗадания = Запрос.Выполнить();
        Выборка = РезультатЗадания.Выбрать();
        Архиватор = Новый ЗаписьZipФайла;
        КаталогФайлов = ПолучитьИмяВременногоФайла();
        ДанныеМанифеста = Новый Массив;
        Пока Выборка.Следующий() Цикл
            ДобавитьОбъектКВыгрузке(УчетнаяСистема, КаталогФайлов, Архиватор, ДанныеМанифеста, Выборка, Выборка.ИдентификаторФайла);
        КонецЦикла;
        Манифест = Новый Структура(Словарь.ИмяПоляСоставаДанных(), ДанныеМанифеста);
        Архиватор.Добавить(ЗаписатьМанифест(Манифест, КаталогФайлов));
        ДвоичныеДанныеАрхива = Архиватор.ПолучитьДвоичныеДанные(,, МетодСжатияZIP.Сжатие, УровеньСжатияZIP.Максимальный);
        ИмяАрхива = Строка(ИдентификаторЗадания) + ".zip";
        ИдентификаторФайлаАрхива = ФайлыОбластейДанных.ЗагрузитьФайл(ИмяАрхива, ДвоичныеДанныеАрхива,, Истина);
        Попытка
            УдалитьФайлы(КаталогФайлов);
		Исключение
			Комментарий = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(Словарь.КорневоеСобытие(), УровеньЖурналаРегистрации.Ошибка, , , Комментарий);
        КонецПопытки;
        РезультатЗадания = Новый Структура;
        РезультатЗадания.Вставить(Словарь.ПолеХранилище(), ФайлыОбластейДанных.ИдентификаторХранилища());
        РезультатЗадания.Вставить(Словарь.ПолеИдентификатор(), Строка(ИдентификаторФайлаАрхива));
        Свойства = РегистрыСведений.СвойстваЗаданий.НовыйСвойстваЗадания();
        Свойства.КодСостояния = СловарьОчередь.КодСостоянияВыполнено();
        Свойства.Результат = РаботаВМоделиСервисаБТС.СтрокаИзСтруктурыJSON(РезультатЗадания);
        РегистрыСведений.СвойстваЗаданий.Установить(ИдентификаторЗадания, Свойства);
    Исключение
        ОчередьЗаданийВнешнийИнтерфейс.УстановитьВнутреннююОшибку(
            ИдентификаторЗадания, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
        ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
    КонецПопытки
    
КонецПроцедуры

// Обработчик задания очереди заданий.
//
// Параметры:
//  УчетнаяСистема - ОпределяемыйТип.УчетныеСистемыИнтеграцииОбластейДанных - учетная система
//                   по которой готовятся данные.
//  ИдентификаторФайла - Строка - идентификатор файла для обработки (длинна 36).
//
Процедура ОбработатьДанные(УчетнаяСистема, ИдентификаторФайла) Экспорт
    
    Словарь = ИнтеграцияОбъектовОбластейДанныхСловарь;
    СловарьОчередь = ОчередьЗаданийВнешнийИнтерфейсСловарь;
    КлючЗадания = КлючЗаданияОбработкиДанных(УчетнаяСистема, ИдентификаторФайла);
    ИдентификаторЗадания = ОчередьЗаданийВнешнийИнтерфейс.ИдентификаторЗадания(КлючЗадания);
    КаталогРаспаковки = ПолучитьИмяВременногоФайла();
    МенеджерУчетнойСистемы = ОбщегоНазначения.МенеджерОбъектаПоСсылке(УчетнаяСистема);
    Попытка
        ОписаниеФайла = ФайлыОбластейДанных.ОписаниеФайла(ИдентификаторФайла);
        Если ЗначениеЗаполнено(ОписаниеФайла.ПолноеИмя) Тогда
            ПотокДанных = ФайловыеПотоки.ОткрытьДляЧтения(ОписаниеФайла.ПолноеИмя);
        Иначе
            ПотокДанных = ОписаниеФайла.Данные.ОткрытьПотокДляЧтения();
        КонецЕсли;
        Архиватор = Новый ЧтениеZipФайла(ПотокДанных);
        Попытка
            Архиватор.ИзвлечьВсе(КаталогРаспаковки);
        Исключение
            ОчередьЗаданийВнешнийИнтерфейс.УстановитьОшибкуДанных(ИдентификаторЗадания, Словарь.ФайлПоврежден());
            Возврат;
        КонецПопытки;
        ПоискМанифеста = НайтиФайлы(КаталогРаспаковки, Словарь.ИмяФайлаМанифеста(), Истина);
        Если ПоискМанифеста.Количество() = 0 Тогда
            ОчередьЗаданийВнешнийИнтерфейс.УстановитьОшибкуДанных(ИдентификаторЗадания, Словарь.МанифестНеЗадан());
            Возврат;
        КонецЕсли;
        Попытка
            ЧтениеJSON = Новый ЧтениеJSON;
            ЧтениеJSON.ОткрытьФайл(ПоискМанифеста[0].ПолноеИмя);
            Манифест = ПрочитатьJSON(ЧтениеJSON);
            ЧтениеJSON.Закрыть();
        Исключение
            ОчередьЗаданийВнешнийИнтерфейс.УстановитьОшибкуДанных(ИдентификаторЗадания, Словарь.МанифестНеВерногоФормата());
            Возврат;
        КонецПопытки;
        Порядок = Словарь.ИмяПоляСоставаДанных();
        Если Не Манифест.Свойство(Порядок) Тогда
            ОчередьЗаданийВнешнийИнтерфейс.УстановитьОшибкуДанных(ИдентификаторЗадания, 
                СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Словарь.ОтсутствуетСвойствоМанифеста(), Порядок));
            Возврат;
        КонецЕсли;
        Результаты = Новый Массив;
        Для Каждого Элемент Из Манифест[Порядок] Цикл
            ИмяФайла = Элемент[Словарь.ПолеФайл()];
            ПоискФайла = НайтиФайлы(КаталогРаспаковки, ИмяФайла);
            Если Элемент.Свойство(Словарь.ПолеВерсия()) Тогда
                Версия = Элемент[Словарь.ПолеВерсия()];
            Иначе 
                Версия = "";
            КонецЕсли; 
            Если ПоискФайла.Количество() = 0 Тогда
                ОчередьЗаданийВнешнийИнтерфейс.УстановитьОшибкуДанных(ИдентификаторЗадания, 
                    СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Словарь.ФайлНеНайден(), ИмяФайла));
                Возврат;
            КонецЕсли;
            ИдентификаторОбъекта = ИмяФайла;
            ПотокДанных = ФайловыеПотоки.ОткрытьДляЧтения(ПоискФайла[0].ПолноеИмя);
            Обработчик = Элемент[Словарь.ПолеОбработчик()];
            КодВозвратаОбработчика = СловарьОчередь.КодСостоянияВыполнено();
            ОшибкаОбработчика = Ложь;
            СообщениеОбОшибкеОбработчика = "";
                
            МенеджерУчетнойСистемы.ОбработатьДанные(УчетнаяСистема, ИдентификаторОбъекта, ПотокДанных, Обработчик, 
                КодВозвратаОбработчика, ОшибкаОбработчика, СообщениеОбОшибкеОбработчика);
            ПотокДанных.Закрыть();
            
            РезультатОбработчика = Новый Структура;
            РезультатОбработчика.Вставить(Словарь.ПолеФайл(), ИмяФайла);
            РезультатОбработчика.Вставить(Словарь.ПолеВерсия(), Версия);
            РезультатОбработчика.Вставить(Словарь.ПолеОбработчик(), Обработчик);
            РезультатОбработчика.Вставить(Словарь.ПолеКодВозврата(), КодВозвратаОбработчика);
            РезультатОбработчика.Вставить(Словарь.ПолеОшибка(), ОшибкаОбработчика);
            РезультатОбработчика.Вставить(Словарь.ПолеСообщениеОбОшибке(), СообщениеОбОшибкеОбработчика);
            Результаты.Добавить(РезультатОбработчика);
            
            ФайлыОбластейДанных.УстановитьПризнакВременного(Новый УникальныйИдентификатор(ИдентификаторФайла));                
        КонецЦикла;
        Попытка
            Если ФайлСуществует(КаталогРаспаковки) Тогда
                УдалитьФайлы(КаталогРаспаковки);
            КонецЕсли; 
		Исключение
			Комментарий = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(Словарь.КорневоеСобытие(), УровеньЖурналаРегистрации.Ошибка, , , Комментарий);
        КонецПопытки;
    Исключение
        ОчередьЗаданийВнешнийИнтерфейс.УстановитьВнутреннююОшибку(
            ИдентификаторЗадания, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
        ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
    КонецПопытки;

    Свойства = РегистрыСведений.СвойстваЗаданий.НовыйСвойстваЗадания();
    Свойства.КодСостояния = СловарьОчередь.КодСостоянияВыполнено();
    Свойства.Результат = РаботаВМоделиСервисаБТС.СтрокаИзСтруктурыJSON(Результаты);
    
    РегистрыСведений.СвойстваЗаданий.Установить(ИдентификаторЗадания, Свойства);
    
КонецПроцедуры

// См. ОчередьЗаданийПереопределяемый.ПриОпределенииПсевдонимовОбработчиков.
//
Процедура ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам) Экспорт
	
	СоответствиеИменПсевдонимам.Вставить("ИнтеграцияОбъектовОбластейДанных.ПодготовитьДанные");
	СоответствиеИменПсевдонимам.Вставить("ИнтеграцияОбъектовОбластейДанных.ОбработатьДанные");
	СоответствиеИменПсевдонимам.Вставить("ИнтеграцияОбъектовОбластейДанных.ВыполнитьКоманду");
	
КонецПроцедуры                                                       

// См. ВыгрузкаЗагрузкаДанныхПереопределяемый.ПриЗаполненииТиповИсключаемыхИзВыгрузкиЗагрузки.
//
Процедура ПриЗаполненииТиповИсключаемыхИзВыгрузкиЗагрузки(Типы) Экспорт
	
	Типы.Добавить(Метаданные.РегистрыСведений.ОбъектыКОтправке);
	Типы.Добавить(Метаданные.РегистрыСведений.НастройкиУчетныхСистем);
    Типы.Добавить(Метаданные.РегистрыСведений.СостоянияИнтеграцииОбъектов);
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Процедура ОтправитьОповещение(ДанныеОповещения, Настройки) 
	
	Словарь = ИнтеграцияОбъектовОбластейДанныхСловарь;
	ЧастиИмени = СтрРазделить(Настройки.ИмяСертификата, ".");	
	ТипСертификата = ЧастиИмени[ЧастиИмени.ВГраница()];
	
	ЧастиАдреса = СтрРазделить(Настройки.АдресСервиса, "/", Ложь);
	
	Протокол = ЧастиАдреса[0];
	ЧастиАдреса.Удалить(0);
	
	ЧастиИмени = СтрРазделить(ЧастиАдреса[0], ":", Ложь);
	Сервер = ЧастиИмени[0];
	Порт = ?(ЧастиИмени.Количество() = 2, ЧастиИмени[1], Неопределено);
	ЧастиАдреса.Удалить(0);
	
	ПутьНаСервере = СтрСоединить(ЧастиАдреса, "/");
	
	СертификатКлиента = Неопределено;
	Если Настройки.ИспользоватьСертификат И ЗначениеЗаполнено(ТипСертификата) Тогда
		ИмяФайлаСертификата = ПолучитьИмяВременногоФайла(ТипСертификата);
		Настройки.ДанныеСертификата.Записать(ИмяФайлаСертификата);
		СертификатКлиента = Новый СертификатКлиентаФайл(ИмяФайлаСертификата, Настройки.ПарольСертификата);
		Попытка
			УдалитьФайлы(ИмяФайлаСертификата);
		Исключение
			Комментарий = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(Словарь.КорневоеСобытие(), УровеньЖурналаРегистрации.Ошибка, , , Комментарий);
		КонецПопытки;
	КонецЕсли;
    Если Настройки.СпособАутентификации = Перечисления.СпособыАутентификации.ОбычнаяПроверка Тогда
        Логин = Настройки.Логин;
        Пароль = Настройки.Пароль;
    Иначе
        Логин = Неопределено;
        Пароль = Неопределено;                                                              
    КонецЕсли; 	
	ЗащищенноеСоединение = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение(СертификатКлиента, Новый СертификатыУдостоверяющихЦентровОС);
	Соединение = Новый HTTPСоединение(Сервер, Порт, Логин, Пароль, , 30, ЗащищенноеСоединение);
	Запрос = Новый HTTPЗапрос(ПутьНаСервере);
	Запрос.УстановитьТелоИзСтроки(ДанныеОповещения);
		
    Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	Если Ответ.КодСостояния <> 200 Тогда
		ТекстИсключения = СтрШаблон("%1 %2", Ответ.КодСостояния, Ответ.ПолучитьТелоКакСтроку());
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьПодтверждения(УчетнаяСистема, ДанныеПодтверждений)
    
    Словарь = ИнтеграцияОбъектовОбластейДанныхСловарь;
    МенеджерУчетнойСистемы = ОбщегоНазначения.МенеджерОбъектаПоСсылке(УчетнаяСистема);
    Идентификаторы = Новый Массив;
    Подтверждения = Новый Соответствие;
    Для Каждого Подтверждение Из ДанныеПодтверждений[Словарь.ПолеРезультат()] Цикл
        Идентификаторы.Добавить(Подтверждение[Словарь.ПолеФайл()]);
        Версия = "";
        Если Подтверждение.Свойство(Словарь.ПолеВерсия()) Тогда
            Версия = Строка(Подтверждение[Словарь.ПолеВерсия()]);
        КонецЕсли;
        КодВозврата = 0;
        Если Подтверждение.Свойство(Словарь.ПолеКодВозврата()) Тогда
            КодВозврата = Подтверждение[Словарь.ПолеКодВозврата()];
		КонецЕсли; 
		ДанныеПодтверждения = Новый Структура;
		ДанныеПодтверждения.Вставить("Версия", Версия); 
		ДанныеПодтверждения.Вставить("КодВозврата", КодВозврата); 
		ДанныеПодтверждения.Вставить("Ошибка", Подтверждение[Словарь.ПолеОшибка()]); 
		ДанныеПодтверждения.Вставить("СообщениеОбОшибке", Подтверждение[Словарь.ПолеСообщениеОбОшибке()]);
        Подтверждения.Вставить(Подтверждение[Словарь.ПолеФайл()], ДанныеПодтверждения);
    КонецЦикла;
    
    Запрос = Новый Запрос;
    Запрос.Текст = 
    	"ВЫБРАТЬ
        |   ОбъектыКОтправке.ИдентификаторОбъекта КАК ИдентификаторОбъекта,
        |   ОбъектыКОтправке.УчетнаяСистема КАК УчетнаяСистема,
        |   ОбъектыКОтправке.ИдентификаторФайла КАК ИдентификаторФайла,
        |   ОбъектыКОтправке.Обработчик КАК Обработчик,
        |   ЕСТЬNULL(ФайлыОбластейДанных.ФайлCRC32, 0) КАК Версия
        |ИЗ
        |   РегистрСведений.ОбъектыКОтправке КАК ОбъектыКОтправке
        |       ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФайлыОбластейДанных КАК ФайлыОбластейДанных
        |       ПО ОбъектыКОтправке.ИдентификаторФайла = ФайлыОбластейДанных.Идентификатор
        |ГДЕ
        |   ОбъектыКОтправке.ИдентификаторОбъекта В(&Идентификаторы)
        |   И ОбъектыКОтправке.УчетнаяСистема = &УчетнаяСистема";
    Запрос.УстановитьПараметр("Идентификаторы", Идентификаторы);
    Запрос.УстановитьПараметр("УчетнаяСистема", УчетнаяСистема);
    Результат = Запрос.Выполнить();
    Выборка = Результат.Выбрать();
    
    СтандартнаяОбработка = Истина;
    ПодтвержденияОбъектов = Новый Соответствие;
    Пока Выборка.Следующий() Цикл
        Подтверждение = Подтверждения.Получить(Выборка.ИдентификаторОбъекта);
        ПодтвержденияОбъектов.Вставить(Выборка.ИдентификаторОбъекта, Подтверждение);
    КонецЦикла;
    
    МенеджерУчетнойСистемы.ОбработатьПодтверждения(УчетнаяСистема, ПодтвержденияОбъектов, СтандартнаяОбработка);
    Если Не СтандартнаяОбработка Тогда
        Возврат;
    КонецЕсли; 
    Выборка.Сбросить();
    
    Пока Выборка.Следующий() Цикл
        Подтверждение = Подтверждения.Получить(Выборка.ИдентификаторОбъекта);
        // Если получено подтверждение с корректной версией объекта, 
        // то снимем объект с отправки и установим признак интеграции объекта.
        Если Не Подтверждение.Ошибка Тогда
            Версия = 0;
            Если ЗначениеЗаполнено(Подтверждение.Версия) 
               И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Подтверждение.Версия) Тогда
                Версия = Число(Подтверждение.Версия);
            КонецЕсли; 
            Если Версия = Выборка.Версия И Выборка.Версия <> 0 Тогда
                Если ЗначениеЗаполнено(Выборка.ИдентификаторФайла) Тогда
                    ФайлыОбластейДанных.УстановитьПризнакВременного(Выборка.ИдентификаторФайла);
                КонецЕсли;
                // Очистим данные к отправке.
                Отбор = Новый Структура;
                Отбор.Вставить("УчетнаяСистема", Выборка.УчетнаяСистема);
                Отбор.Вставить("ИдентификаторОбъекта", Выборка.ИдентификаторОбъекта);
                Отбор.Вставить("Обработчик", Выборка.Обработчик);
                РегистрыСведений.ОбъектыКОтправке.УдалитьЗаписи(Отбор);
                // Установим состояние интегрированного объекта.
                ДанныеЗаписи = РегистрыСведений.СостоянияИнтеграцииОбъектов.НовыйДанныеЗаписи();
                ДанныеЗаписи.УчетнаяСистема = Выборка.УчетнаяСистема;
                ДанныеЗаписи.ИдентификаторОбъекта = Выборка.ИдентификаторОбъекта;
                ДанныеЗаписи.Состояние = Перечисления.СостоянияИнтеграцииОбъектов.Интегрирован;
                РегистрыСведений.СостоянияИнтеграцииОбъектов.УстановитьСостояние(ДанныеЗаписи);
            КонецЕсли; 
        Иначе 
            // Ошибка при подтверждении, установим признак ошибки интеграции объекта.
            // Объект не будет передаваться, пока по нему не будут сформированы новые данные к отправке.
            ДанныеЗаписи = РегистрыСведений.СостоянияИнтеграцииОбъектов.НовыйДанныеЗаписи();
            ДанныеЗаписи.УчетнаяСистема = Выборка.УчетнаяСистема;
            ДанныеЗаписи.ИдентификаторОбъекта = Выборка.ИдентификаторОбъекта;
            ДанныеЗаписи.Состояние = Перечисления.СостоянияИнтеграцииОбъектов.Ошибка;
            ДанныеЗаписи.Описание = Подтверждение.СообщениеОбОшибке; 
            РегистрыСведений.СостоянияИнтеграцииОбъектов.УстановитьСостояние(ДанныеЗаписи);
        КонецЕсли;
    КонецЦикла;
	
КонецПроцедуры

// Добавляет объект в состав объектов к выгрузке в файл ответа.
//
// Параметры:
//  УчетнаяСистема - ОпределяемыйТип.УчетныеСистемыИнтеграцииОбластейДанных - учетная система по которой готовятся данные.
//  КаталогФайлов - Строка - каталог для сохранения файлов, добавляемых в архив.  
//  Архиватор - ЗаписьZipФайла - для добавления файлов к выгрузке
//  ДанныеМанифеста - Массив - для добавления информации о файлах в манифест. 
//  Параметры - Структура, ВыборкаИзРезультатаЗапроса - параметры добавляемого объекта.
//    * ИдентификаторОбъекта - Строка
//    * Обработчик - Строка
//  ИдентификаторФайла - ИдентификаторФайла - если пустой, данные будут запрошены из учетной системы. 
//
Процедура ДобавитьОбъектКВыгрузке(УчетнаяСистема, КаталогФайлов, Архиватор, ДанныеМанифеста, Параметры, Знач ИдентификаторФайла = Неопределено)
    
    Если Не ФайлСуществует(КаталогФайлов) Тогда
        СоздатьКаталог(КаталогФайлов);
    КонецЕсли; 

    Если Не ЗначениеЗаполнено(ИдентификаторФайла) Тогда
        МенеджерУчетнойСистемы = ОбщегоНазначения.МенеджерОбъектаПоСсылке(УчетнаяСистема);
        ДанныеОбъекта = МенеджерУчетнойСистемы.ДанныеОбъекта(УчетнаяСистема, Параметры.ИдентификаторОбъекта, Параметры.Обработчик);
        Если Не ЗначениеЗаполнено(ДанныеОбъекта) Тогда
            УдалитьОбъектКОтправке(УчетнаяСистема, Параметры.ИдентификаторОбъекта);
            Возврат;
        КонецЕсли;
        ИдентификаторФайла = ФайлыОбластейДанных.ЗагрузитьФайл(Параметры.ИдентификаторОбъекта, ДанныеОбъекта);
        ЗарегистрироватьОтправку(УчетнаяСистема, Параметры.ИдентификаторОбъекта, Параметры.Обработчик, ИдентификаторФайла);
    КонецЕсли;
    ОписаниеФайла = ФайлыОбластейДанных.ОписаниеФайла(ИдентификаторФайла);
    Если ЗначениеЗаполнено(ОписаниеФайла.ПолноеИмя) Тогда
        Архиватор.Добавить(ОписаниеФайла.ПолноеИмя);
    Иначе
        ИмяФайла = КаталогФайлов + ПолучитьРазделительПутиСервера() + Параметры.ИдентификаторОбъекта;
        ОписаниеФайла.Данные.Записать(ИмяФайла);
        Архиватор.Добавить(ИмяФайла);
    КонецЕсли;
    СвойстваОбъекта = СвойстваОбъекта(Параметры.ИдентификаторОбъекта, Параметры.Обработчик, ОписаниеФайла.CRC32);
    ДанныеМанифеста.Добавить(СвойстваОбъекта);
	
КонецПроцедуры
 
#Область ПодготовкаДанныхКОтправке
    
Процедура ЗарегистрироватьОтправку(УчетнаяСистема, ИдентификаторОбъекта, Обработчик, ИдентификаторФайла = Неопределено)
    
    ДанныеЗаписи = РегистрыСведений.ОбъектыКОтправке.НовыйДанныеЗаписи();
    ДанныеЗаписи.УчетнаяСистема = УчетнаяСистема;
    ДанныеЗаписи.ИдентификаторОбъекта = ИдентификаторОбъекта;
    ДанныеЗаписи.Обработчик = Обработчик;
    Если ЗначениеЗаполнено(ИдентификаторФайла) Тогда
        ДанныеЗаписи.ИдентификаторФайла = ИдентификаторФайла;
    КонецЕсли; 
    РегистрыСведений.ОбъектыКОтправке.ДобавитьЗапись(ДанныеЗаписи);
    
    ДанныеЗаписи = РегистрыСведений.СостоянияИнтеграцииОбъектов.НовыйДанныеЗаписи();
    ДанныеЗаписи.УчетнаяСистема = УчетнаяСистема;
    ДанныеЗаписи.ИдентификаторОбъекта = ИдентификаторОбъекта;
    ДанныеЗаписи.Состояние = Перечисления.СостоянияИнтеграцииОбъектов.Ожидание;
    РегистрыСведений.СостоянияИнтеграцииОбъектов.УстановитьСостояние(ДанныеЗаписи);
    
КонецПроцедуры

Функция СвойстваОбъекта(Идентификатор, Обработчик, Версия)
    
    Словарь = ИнтеграцияОбъектовОбластейДанныхСловарь;
    СвойстваОбъекта = Новый Структура;
    СвойстваОбъекта.Вставить(Словарь.ПолеФайл(), Идентификатор);
    СвойстваОбъекта.Вставить(Словарь.ПолеОбработчик(), Обработчик);
    СвойстваОбъекта.Вставить(Словарь.ПолеВерсия(), Формат(Версия, "ЧГ=0"));
    
    Возврат СвойстваОбъекта;
    
КонецФункции

Функция ФайлСуществует(ПолноеИмяФайла)
    
	Файл = Новый Файл(ПолноеИмяФайла);
	Возврат Файл.Существует();
    
КонецФункции

Функция ЗаписатьМанифест(ДанныеМанифеста, ВременныйКаталог)
    
    Словарь = ИнтеграцияОбъектовОбластейДанныхСловарь;
    ИмяФайла = СтрШаблон("%1%2%3",
        ВременныйКаталог, ПолучитьРазделительПутиСервера(),
        Словарь.ИмяФайлаМанифеста());
    
	ЗаписьJSON = Новый ЗаписьJSON;
    ЗаписьJSON.ОткрытьФайл(ИмяФайла);
    ЗаписатьJSON(ЗаписьJSON, ДанныеМанифеста);
    ЗаписьJSON.Закрыть();
    
    Возврат ИмяФайла;
	
КонецФункции

#КонецОбласти

#Область СтандартныеОтветы
    
Функция ОтветПоУмолчанию()
	
    Словарь = ИнтеграцияОбъектовОбластейДанныхСловарь;
    ОсновнойРаздел = Новый Структура;
    ОсновнойРаздел.Вставить(Словарь.ПолеКодВозврата(), Словарь.КодВозвратаВыполнено());
    ОсновнойРаздел.Вставить(Словарь.ПолеОшибка(), Ложь);
    ОсновнойРаздел.Вставить(Словарь.ПолеСообщениеОбОшибке(), "");
    ОтветПоУмолчанию = Новый Структура;
    ОтветПоУмолчанию.Вставить(Словарь.ПолеОсновнойРаздел(), ОсновнойРаздел);
    ОтветПоУмолчанию.Вставить(Словарь.ПолеРезультат());
    
    Возврат ОтветПоУмолчанию;
    
КонецФункции

Процедура УстановитьОшибкуДанных(Ответ, СообщениеОбОшибке)
    
    Словарь = ИнтеграцияОбъектовОбластейДанныхСловарь;
    Ответ[Словарь.ПолеОсновнойРаздел()][Словарь.ПолеКодВозврата()] = Словарь.КодВозвратаОшибкаДанных();
    Ответ[Словарь.ПолеОсновнойРаздел()][Словарь.ПолеОшибка()] = Истина;
    Ответ[Словарь.ПолеОсновнойРаздел()][Словарь.ПолеСообщениеОбОшибке()] = СообщениеОбОшибке;
    УдалитьПустойРезультат(Ответ);	
    
КонецПроцедуры

Процедура УстановитьВнутреннююОшибку(Ответ, СообщениеОбОшибке)
    
    Словарь = ИнтеграцияОбъектовОбластейДанныхСловарь;
    Ответ[Словарь.ПолеОсновнойРаздел()][Словарь.ПолеКодВозврата()] = Словарь.КодВозвратаВнутренняяОшибка();    
    Ответ[Словарь.ПолеОсновнойРаздел()][Словарь.ПолеОшибка()] = Истина;
    Ответ[Словарь.ПолеОсновнойРаздел()][Словарь.ПолеСообщениеОбОшибке()] = СообщениеОбОшибке;
    УдалитьПустойРезультат(Ответ);
	
КонецПроцедуры

Процедура УстановитьОтсутствиеДанных(Ответ)
    
    Словарь = ИнтеграцияОбъектовОбластейДанныхСловарь;
    Ответ[Словарь.ПолеОсновнойРаздел()][Словарь.ПолеКодВозврата()] = Словарь.КодВозвратаНеНайдено();    
    УдалитьПустойРезультат(Ответ);	
    
КонецПроцедуры

Процедура УдалитьПустойРезультат(Ответ)
	
    ПолеРезультат = ИнтеграцияОбъектовОбластейДанныхСловарь.ПолеРезультат();
    Если Ответ.Свойство(ПолеРезультат) И Не ЗначениеЗаполнено(Ответ[ПолеРезультат]) Тогда
        Ответ.Удалить(ПолеРезультат);
    КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#Область ПланированиеЗаданий

// Добавляет задание выполнения команды в очередь заданий
//
// Параметры:
//  УчетнаяСистема - ОпределяемыйТип.УчетныеСистемыИнтеграцииОбластейДанных - учетная система.
//  ПараметрыКоманды - ДвоичныеДанные - двоичные данные параметров команды.
//  ИдентификаторПараметров - УникальныйИдентификатор - Идентификатор файла параметров команды.
//
Функция ДобавитьЗаданиеВыполненияКоманды(УчетнаяСистема, ПараметрыКоманды, ИдентификаторПараметров)
    
    ИмяМетода = "ВыполнитьКоманду";
    Параметры = Новый Массив();
    Параметры.Добавить(УчетнаяСистема);
    Параметры.Добавить(ИдентификаторПараметров);
    КлючЗадания = КлючЗаданияВыполненияКоманды(УчетнаяСистема, ПараметрыКоманды);
    ПараметрыЗадания = НовыйПараметрыЗадания(ИмяМетода, КлючЗадания, Параметры);
    
    Возврат ОчередьЗаданий.ДобавитьЗадание(ПараметрыЗадания);
    
КонецФункции    

// Добавляет задание подготовки данных в очередь заданий
//
// Параметры:
//  УчетнаяСистема - ОпределяемыйТип.УчетныеСистемыИнтеграцииОбластейДанных - учетная система.
//
Функция ДобавитьЗаданиеПодготовкиДанных(УчетнаяСистема)
    
    ИмяМетода = "ПодготовитьДанные";
    Параметры = Новый Массив();
    Параметры.Добавить(УчетнаяСистема);
    КлючЗадания = КлючЗаданияПодготовкиДанных(УчетнаяСистема);
    ПараметрыЗадания = НовыйПараметрыЗадания(ИмяМетода, КлючЗадания, Параметры);
    
    Возврат ОчередьЗаданий.ДобавитьЗадание(ПараметрыЗадания);
	
КонецФункции

// Добавляет задание обработки данных в очередь заданий
//
// Параметры:
//  УчетнаяСистема - ОпределяемыйТип.УчетныеСистемыИнтеграцииОбластейДанных - учетная система.
//  ИдентификаторФайла - Строка - идентификатор файла для обработки (длинна 36).
//
Функция ДобавитьЗаданиеОбработкиДанных(УчетнаяСистема, ИдентификаторФайла)
    
    ИмяМетода = "ОбработатьДанные";
    Параметры = Новый Массив();
    Параметры.Добавить(УчетнаяСистема);
    Параметры.Добавить(ИдентификаторФайла);
    КлючЗадания = КлючЗаданияОбработкиДанных(УчетнаяСистема, ИдентификаторФайла);
    
    ПараметрыЗадания = НовыйПараметрыЗадания(ИмяМетода, КлючЗадания, Параметры);
    
    Возврат ОчередьЗаданий.ДобавитьЗадание(ПараметрыЗадания);
    
КонецФункции

Функция КлючЗаданияВыполненияКоманды(УчетнаяСистема, Настройки)
    
    Словарь = ИнтеграцияОбъектовОбластейДанныхСловарь;
    ИдентификаторСистемы = Строка(УчетнаяСистема.УникальныйИдентификатор());
    Хэширование = Новый ХешированиеДанных(ХешФункция.SHA1);
    Хэширование.Добавить(Настройки);
    ХэшНастроек = Base64Строка(Хэширование.ХешСумма);
    
    Возврат СтрШаблон("%1/%2/%3", ИдентификаторСистемы, Словарь.КомандаВыполнить(), ХэшНастроек);
	
КонецФункции
 
Функция КлючЗаданияПодготовкиДанных(УчетнаяСистема)
    
    Словарь = ИнтеграцияОбъектовОбластейДанныхСловарь;
    Возврат СтрШаблон("%1/%2", Строка(УчетнаяСистема.УникальныйИдентификатор()), Словарь.КомандаПолучить());
	
КонецФункции

Функция КлючЗаданияОбработкиДанных(УчетнаяСистема, ИдентификаторФайла)
	
    Словарь = ИнтеграцияОбъектовОбластейДанныхСловарь;
    Возврат СтрШаблон("%1/%2/%3", Строка(УчетнаяСистема.УникальныйИдентификатор()), Словарь.КомандаОтправить(), ИдентификаторФайла);
	
КонецФункции

Функция СвойстваЗадания(Задание)
    
    Ответ = ОтветПоУмолчанию();
    Словарь = ИнтеграцияОбъектовОбластейДанныхСловарь;	
    ИдентификаторЗадания = Задание.УникальныйИдентификатор();
    РегистрыСведений.СвойстваЗаданий.Установить(Задание);
    Результат = Новый Структура;
    Результат.Вставить(Словарь.ПолеХранилище(), ОчередьЗаданийВнешнийИнтерфейс.ИдентификаторХранилища());
    Результат.Вставить(Словарь.ПолеИдентификатор(), Строка(ИдентификаторЗадания));
    Ответ[Словарь.ПолеРезультат()] = Результат;
    
    Возврат Ответ;
	
КонецФункции

Функция НовыйПараметрыЗадания(ИмяМетода, Ключ, Параметры = Неопределено)
	
    ПараметрыЗадания = Новый Структура;
    ПараметрыЗадания.Вставить("Ключ", Ключ);
    ПараметрыЗадания.Вставить("ИмяМетода", СтрШаблон("ИнтеграцияОбъектовОбластейДанных.%1", ИмяМетода));
    ПараметрыЗадания.Вставить("Параметры", Параметры);
    ПараметрыЗадания.Вставить("ОбластьДанных", РаботаВМоделиСервиса.ЗначениеРазделителяСеанса());
    ПараметрыЗадания.Вставить("Использование", Истина);
    ПараметрыЗадания.Вставить("ЭксклюзивноеВыполнение", Истина);
    ПараметрыЗадания.Вставить("ИнтервалПовтораПриАварийномЗавершении", 0);
    ПараметрыЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", 0);
    
    Возврат ПараметрыЗадания;
	
КонецФункции
 
#КонецОбласти 

#Область СозданиеПолучениеУчетнойСистемы
    
Функция АвторизованнаяУчетнаяСистема()
	
    УчетнаяСистема = Неопределено;
    
    Запрос = Новый Запрос;
    Запрос.Текст = 
    	"ВЫБРАТЬ ПЕРВЫЕ 1
        |   НастройкиУчетныхСистем.УчетнаяСистема КАК УчетнаяСистема
        |ИЗ
        |   РегистрСведений.НастройкиУчетныхСистем КАК НастройкиУчетныхСистем
        |ГДЕ
        |   НастройкиУчетныхСистем.Пользователь = &Пользователь
        |   И НЕ НастройкиУчетныхСистем.УчетнаяСистема.ПометкаУдаления";
    
    Запрос.УстановитьПараметр("Пользователь",Пользователи.ТекущийПользователь());
    Результат = Запрос.Выполнить();
    Выборка = Результат.Выбрать();
    
    Если Выборка.Следующий() Тогда
        УчетнаяСистема = Выборка.УчетнаяСистема;
    КонецЕсли;
    
    Возврат УчетнаяСистема;	
	
КонецФункции

Функция НоваяУчетнаяСистема(МетаданныеУчетнойСистемы, МенеджерУчетнойСистемы)
    
    Словарь = ИнтеграцияОбъектовОбластейДанныхСловарь;
    Если ОбщегоНазначения.ЭтоСправочник(МетаданныеУчетнойСистемы) 
        Или ОбщегоНазначения.ЭтоПланВидовХарактеристик(МетаданныеУчетнойСистемы) Тогда
        Возврат МенеджерУчетнойСистемы.СоздатьЭлемент();
    ИначеЕсли ОбщегоНазначения.ЭтоДокумент(МетаданныеУчетнойСистемы) Тогда
        Возврат МетаданныеУчетнойСистемы.СоздатьДокумент();
    Иначе
        ВызватьИсключение Словарь.ПоддерживаютсяТипы();
    КонецЕсли;
	
КонецФункции
 
#КонецОбласти

#Область НастройкиУчетнойСистемы

Функция УстановитьНастройки(УчетнаяСистема, ДанныеНастроек, Ответ)
    
    Словарь = ИнтеграцияОбъектовОбластейДанныхСловарь;
    ПотокДанных = ФайловыеПотоки.ОткрытьДляЧтения(ДанныеНастроек);
    Если ПотокДанных.Размер() = 0 Тогда
        УстановитьОшибкуДанных(Ответ, Словарь.НастройкиНеЗаданы());
        Возврат Ответ;
    КонецЕсли; 
    Настройки = РаботаВМоделиСервисаБТС.СтруктураИзПотокаJSON(ПотокДанных);
    Если Не ЗначениеЗаполнено(УчетнаяСистема) Тогда
        УчетнаяСистемаОпределена = Ложь;
        ИмяСвойства = "type";
        Если Настройки.Свойство(ИмяСвойства) Тогда
            ТипУчетнойСистемы = Настройки[ИмяСвойства];
        Иначе
            УстановитьОшибкуДанных(Ответ, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
                Словарь.ОтсутствуетСвойство(), ИмяСвойства)); 
            Возврат Ответ;
        КонецЕсли;        
        Для Каждого Тип Из Метаданные.ОпределяемыеТипы.УчетныеСистемыИнтеграцииОбластейДанных.Тип.Типы() Цикл
            МетаданныеУчетнойСистемы = Метаданные.НайтиПоТипу(Тип);
            ИмяМетаданныхСистемы = МетаданныеУчетнойСистемы.ПолноеИмя();
            МенеджерУчетнойСистемы = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ИмяМетаданныхСистемы);
            Если НРег(МенеджерУчетнойСистемы.ТипУчетнойСистемы()) = НРег(ТипУчетнойСистемы) Тогда
                УчетнаяСистемаОпределена = Истина;
                Прервать;
            КонецЕсли; 
        КонецЦикла;
        Если Не УчетнаяСистемаОпределена Тогда
            УстановитьОшибкуДанных(Ответ, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
            Словарь.НеОпределенаУчетнаяСистемаПоТипу(), ИмяСвойства));
                Возврат Ответ;
        КонецЕсли;
        Объект = НоваяУчетнаяСистема(МетаданныеУчетнойСистемы, МенеджерУчетнойСистемы);
        ИмяСвойства = "name";
        Если Настройки.Свойство(ИмяСвойства) Тогда
            Объект.Наименование = Настройки[ИмяСвойства];
        КонецЕсли; 
        Объект.Записать();
        УчетнаяСистема = Объект.Ссылка;
        МенеджерНастроек = РегистрыСведений.НастройкиУчетныхСистем.СоздатьМенеджерЗаписи();
        МенеджерНастроек.УчетнаяСистема = УчетнаяСистема;
        МенеджерНастроек.Пользователь = Пользователи.ТекущийПользователь();
        МенеджерНастроек.Записать();
    Иначе
        МетаданныеУчетнойСистемы = УчетнаяСистема.Метаданные();
        МенеджерУчетнойСистемы = ОбщегоНазначения.МенеджерОбъектаПоСсылке(УчетнаяСистема);
        МенеджерНастроек = РегистрыСведений.НастройкиУчетныхСистем.СоздатьМенеджерЗаписи();
        МенеджерНастроек.УчетнаяСистема = УчетнаяСистема;
        МенеджерНастроек.Прочитать();
        Если Не МенеджерНастроек.Выбран() Тогда
            МенеджерНастроек.УчетнаяСистема = УчетнаяСистема;
            МенеджерНастроек.Пользователь = Пользователи.ТекущийПользователь();
        КонецЕсли; 
    КонецЕсли;
    
    ЗаполняемыеНастройки = ЗаполняемыеНастройки();
    Для Каждого Настройка Из ЗаполняемыеНастройки Цикл
        ПутьНастройки = СтрРазделить(Настройка.Путь, ".", Ложь);
        Значение = Настройки;
        ЗначениеОпределено = Истина;
        Для Каждого ЧастьПути Из ПутьНастройки Цикл
            Если Значение.Свойство(ЧастьПути) Тогда
                Значение = Значение[ЧастьПути];
            Иначе
                ЗначениеОпределено = Ложь;
                Прервать;
            КонецЕсли; 
        КонецЦикла;
        Если Не ЗначениеОпределено Тогда
            Продолжить;
        КонецЕсли; 
        Значение = ПривестиЗначение(Настройка.Тип, Значение);
        Если Настройка.БезопасноеХранение Тогда
            УстановитьПривилегированныйРежим(Истина);
            ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(УчетнаяСистема, Значение, Настройка.ИмяСвойства);
            УстановитьПривилегированныйРежим(Ложь);
        ИначеЕсли Метаданные.РегистрыСведений.НастройкиУчетныхСистем.Ресурсы.Найти(Настройка.ИмяСвойства) <> Неопределено Тогда
            МенеджерНастроек[Настройка.ИмяСвойства] = Значение;
        КонецЕсли; 
    КонецЦикла;
    МенеджерНастроек.Записать();
    
    ПотокДанных = ФайловыеПотоки.ОткрытьДляЧтения(ДанныеНастроек);
    МенеджерУчетнойСистемы.УстановитьНастройки(УчетнаяСистема, ПотокДанных, 
        Ответ[Словарь.ПолеОсновнойРаздел()][Словарь.ПолеКодВозврата()], 
        Ответ[Словарь.ПолеОсновнойРаздел()][Словарь.ПолеОшибка()], 
        Ответ[Словарь.ПолеОсновнойРаздел()][Словарь.ПолеСообщениеОбОшибке()]);
        
    УдалитьПустойРезультат(Ответ);
    Возврат Ответ;
	
КонецФункции
    
Функция ЗаполняемыеНастройки(КлючиНастроек = Неопределено)
	
	Настройки = Новый ТаблицаЗначений;
    Настройки.Колонки.Добавить("Путь");
    Настройки.Колонки.Добавить("ИмяСвойства");
    Настройки.Колонки.Добавить("Тип");
    Настройки.Колонки.Добавить("БезопасноеХранение");
    
    ВозвращаемыеНастройки = Настройки.Скопировать();

    ДобавитьНастройку(Настройки, "use_notices", "ОповещатьОбИзменениях", Новый ОписаниеТипов("Булево"));
    ДобавитьНастройку(Настройки, "notice_settings.url", "АдресСервиса", Новый ОписаниеТипов("Строка"));
    ДобавитьНастройку(Настройки, "notice_settings.authentication_type", "СпособАутентификации", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыАутентификации"));
    ДобавитьНастройку(Настройки, "notice_settings.login", "Логин", Новый ОписаниеТипов("Строка"));
    ДобавитьНастройку(Настройки, "notice_settings.password", "Пароль", Новый ОписаниеТипов("Строка"), Истина);
    ДобавитьНастройку(Настройки, "notice_settings.use_certificate", "ИспользоватьСертификат", Новый ОписаниеТипов("Булево"));
    ДобавитьНастройку(Настройки, "notice_settings.certificate_name", "ИмяСертификата", Новый ОписаниеТипов("Строка"));
    ДобавитьНастройку(Настройки, "notice_settings.certificate_password", "ПарольСертификата", Новый ОписаниеТипов("Строка"), Истина);
    ДобавитьНастройку(Настройки, "notice_settings.certificate_data", "ДанныеСертификата", Новый ОписаниеТипов("ДвоичныеДанные"), Истина);
    ДобавитьНастройку(Настройки, "notice_settings.use_sign", "ПодписыватьДанные", Новый ОписаниеТипов("Булево"));
    ДобавитьНастройку(Настройки, "notice_settings.sign_key", "КлючПодписи", Новый ОписаниеТипов("Строка"), Истина);
    
    Настройки.Индексы.Добавить("БезопасноеХранение");
    
    Если КлючиНастроек = Неопределено Тогда
        Возврат Настройки;
    Иначе
        Для Каждого Настройка Из Настройки Цикл
            Если КлючиНастроек.Найти(Настройка.ИмяСвойства) <> Неопределено Тогда
                НоваяСтрока = ВозвращаемыеНастройки.Добавить();
                ЗаполнитьЗначенияСвойств(НоваяСтрока, Настройка);
            КонецЕсли; 
        КонецЦикла; 
        ВозвращаемыеНастройки.Индексы.Добавить("БезопасноеХранение");
        Возврат ВозвращаемыеНастройки;
    КонецЕсли; 
	
КонецФункции

Процедура ДобавитьНастройку(Настройки, Путь, ИмяСвойства, Тип, БезопасноеХранение = Ложь)
	
	НоваяНастройка = Настройки.Добавить();
    НоваяНастройка.Путь = Путь;
    НоваяНастройка.ИмяСвойства = ИмяСвойства;
    НоваяНастройка.Тип = Тип;
    НоваяНастройка.БезопасноеХранение = БезопасноеХранение;
	
КонецПроцедуры

Функция ПривестиЗначение(ОписаниеТипа, Значение)
    
    Если ОписаниеТипа.СодержитТип(Тип("ДвоичныеДанные")) И ЗначениеЗаполнено(Значение) Тогда
        Значение = ПолучитьДвоичныеДанныеИзBase64Строки(Значение);
    ИначеЕсли ОписаниеТипа.СодержитТип(Тип("ПеречислениеСсылка.СпособыАутентификации")) Тогда
        Если НРег(Значение) = НРег("basic") Тогда
            Значение = Перечисления.СпособыАутентификации.ОбычнаяПроверка;
        ИначеЕсли НРег(Значение) = НРег("anonymous") Тогда
            Значение = Перечисления.СпособыАутентификации.Анонимный;
        КонецЕсли; 
    КонецЕсли; 
    
    Значение = ОписаниеТипа.ПривестиЗначение(Значение);
    
    Возврат Значение;
    
КонецФункции

#КонецОбласти 

#Область Криптография

Процедура ПодписатьДанные(СвойстваОбъекта, Ключ)
	
    Свойства = Новый Массив;
    Данные = Новый Массив;
    Для Каждого Свойство Из СвойстваОбъекта Цикл
        Свойства.Добавить(Свойство.Ключ);
        Данные.Добавить(Свойство.Значение);
    КонецЦикла; 
    СвойстваОбъекта.Вставить("sign_order", СтрСоединить(Свойства, ","));
    СвойстваОбъекта.Вставить("sign", РаботаВМоделиСервисаБТС.Подпись(Ключ, СтрСоединить(Данные,"")));
	
КонецПроцедуры
    
#КонецОбласти 

#КонецОбласти 
