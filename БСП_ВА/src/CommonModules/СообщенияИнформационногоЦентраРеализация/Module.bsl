////////////////////////////////////////////////////////////////////////////////
// ОБЩАЯ РЕАЛИЗАЦИЯ ОБРАБОТКИ СООБЩЕНИЙ ИНФОРМАЦИОННОГО ЦЕНТРА
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// По сообщению формирует элемент справочника "ОбщиеДанныеИнформационногоЦентра"
//
// Параметры:
//	ТелоСообщения - ОбъектXDTO - тело сообщения.
//
Процедура ДобавитьОповещениеОПожелании(ТелоСообщения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		ДобавитьОповещениеОПожеланииВСправочник(ТелоСообщения);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Добавляет оповещение по пожеланию
//
// Параметры:
//	ТелоСообщения - ОбъектXDTO - тело сообщения.
//
Процедура ДобавитьОповещениеОПожеланииВСправочник(ТелоСообщения)
	
	УведомлениеОПожелании                           = Справочники.ОбщиеДанныеИнформационногоЦентра.СоздатьЭлемент();
	УведомлениеОПожелании.Идентификатор             = ТелоСообщения.id;
	УведомлениеОПожелании.Наименование              = ТелоСообщения.name;
	УведомлениеОПожелании.ДатаНачалаАктуальности    = ТелоСообщения.startDate;
	УведомлениеОПожелании.ДатаОкончанияАктуальности = ТелоСообщения.endDate;
	УведомлениеОПожелании.ТипИнформации             = ИнформационныйЦентрСервер.ОпределитьСсылкуТипаИнформации("УведомлениеОПожелании");
	УведомлениеОПожелании.Критичность               = ТелоСообщения.criticality;
	УведомлениеОПожелании.Записать();
	
КонецПроцедуры

#КонецОбласти


