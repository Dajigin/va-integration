//@strict-types
#Область СлужебныйПрограммныйИнтерфейс	

// Объект тестирования при записи.
// 
// Параметры:
//  Источник - ДокументОбъект - Источник
//  Отказ - Булево - Отказ
Процедура ОбъектТестированияПриЗаписи(Источник, Отказ) Экспорт
	Если Не ПараметрыСеанса.ЭтоАвтоТестирование
		Или Не Источник.ДополнительныеСвойства.ЭтоНовыйОбъект Тогда  
		Возврат;
	КонецЕсли;

	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить();
	ЭлементБлокировки.Область = "РегистрСведений.СозданныеОбъектыТестирования";
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	
	ТекущийМаксНомер = МаксимальныйНомерОбъектаСозданногоПриТестировании();
	НовыйНомерЗаписи = ТекущийМаксНомер + 1;

	
	Менеджер = РегистрыСведений.СозданныеОбъектыТестирования.СоздатьМенеджерЗаписи();
	Менеджер.НомерЗаписи = НовыйНомерЗаписи;
	Менеджер.УникальныйИдентификатор = Источник.Ссылка.УникальныйИдентификатор();
	Менеджер.ИмяТипа = Источник.Метаданные().ПолноеИмя();
	Менеджер.Записать();
КонецПроцедуры

// Параметры:
//  Источник - ДокументОбъект - Источник
//  Отказ - Булево - Отказ
//  РежимЗаписи - РежимЗаписиДокумента - Режим записи
//  РежимПроведения - РежимПроведенияДокумента - Режим проведения
Процедура ОбъектТестированияДокументПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	ОбъектТестированияПередЗаписью(Источник);
КонецПроцедуры

// Объект тестирования бизнес процесс перед записью перед записью.
// 
// Параметры:
//  Источник - ЗадачаОбъект, БизнесПроцессОбъект - Источник
//  Отказ - Булево - Отказ
Процедура ОбъектТестированияКромеДокументаИРегистраПередЗаписью(Источник, Отказ) Экспорт
	ОбъектТестированияПередЗаписью(Источник);
КонецПроцедуры

// Установка параметров сеанса.
// 
// Параметры:
//  ИмяПараметра - Строка - Имя параметра.
//  УстановленныеПараметры - Массив из Строка - Установленные параметры
Процедура УстановкаПараметровСеанса(ИмяПараметра, УстановленныеПараметры) Экспорт
	Если ИмяПараметра = "ЭтоАвтоТестирование" Тогда
		ПараметрЗапускаПриложения = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("ПараметрЗапуска"); // Строка -
		ПараметрыСеанса.ЭтоАвтоТестирование =  (СтрНайти(ПараметрЗапускаПриложения, "АвтоТестирование") > 0);
		УстановленныеПараметры.Добавить("ЭтоАвтоТестирование");
	КонецЕсли;
	
КонецПроцедуры

// Возвращает описание объектов, созданных при тестировании.
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//	* НомерЗаписи - Число
//	* УникальныйИдентификатор - УникальныйИдентификатор - ГУИД объекта
//	* ИмяТипа - Строка - полное имя типа объекта. Например, "Документ._ДемоЗаказПокупателя"
Функция ОбъектыСозданныеПриТестировании() Экспорт
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	СозданныеОбъектыТестирования.НомерЗаписи КАК НомерЗаписи,
	|	СозданныеОбъектыТестирования.УникальныйИдентификатор КАК УникальныйИдентификатор,
	|	СозданныеОбъектыТестирования.ИмяТипа КАК ИмяТипа
	|ИЗ
	|	РегистрСведений.СозданныеОбъектыТестирования КАК СозданныеОбъектыТестирования
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерЗаписи УБЫВ");

	Выборка = Запрос.Выполнить().Выбрать();
	Возврат Выборка;
КонецФункции

// Удалить регистрацию в регистре учета объектов, созданных при тестировании.
// 
// Параметры:
//  НомерЗаписи - Число - Номер записи
//  Идентификатор - УникальныйИдентификатор - Идентификатор
Процедура УдалитьРегистрациюСозданногоОбъектаТестирования(НомерЗаписи, Идентификатор) Экспорт
	МенеджерЗаписи = РегистрыСведений.СозданныеОбъектыТестирования.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.НомерЗаписи = НомерЗаписи;
	МенеджерЗаписи.УникальныйИдентификатор = Идентификатор;
	МенеджерЗаписи.Удалить();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Параметры:
//  Источник - ДокументОбъект - 
//			 - БизнесПроцессОбъект -
//			 - ЗадачаОбъект -
//			 - СправочникОбъект -
//			 - ПланВидовХарактеристикОбъект -
Процедура ОбъектТестированияПередЗаписью(Источник) Экспорт
	Источник.ДополнительныеСвойства.Вставить("ЭтоНовыйОбъект", НЕ ЗначениеЗаполнено(Источник.Ссылка));
КонецПроцедуры
// Максимальный номер объекта, созданного при тестировании.
// 
// Возвращаемое значение:
//  Число - Максимальный номер объекта
Функция МаксимальныйНомерОбъектаСозданногоПриТестировании()
	ЗапросМаксНомер = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	|	СозданныеОбъектыТестирования.НомерЗаписи КАК НомерЗаписи
	|ИЗ
	|	РегистрСведений.СозданныеОбъектыТестирования КАК СозданныеОбъектыТестирования
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерЗаписи УБЫВ");
	РезультатЗапросаМаксНомер = ЗапросМаксНомер.Выполнить();
	Если РезультатЗапросаМаксНомер.Пустой() Тогда
		Возврат 0;
	КонецЕсли;
	Выборка = РезультатЗапросаМаксНомер.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.НомерЗаписи;	
КонецФункции
#КонецОбласти