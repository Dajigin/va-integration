#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Добавляет запись объекта к отправке в регистр.
//
// Параметры:
//  ДанныеЗаписи - Структура - данные для записи с полями:
//      * УчетнаяСистема - см. ОпределяемыйТип.УчетныеСистемыИнтеграцииОбластейДанных - (обязательное) учетная система.
//      * ИдентификаторОбъекта - Строка - (обязательное) идентификатор объекта (длина 50).
//      * Обработчик - Строка - (обязательное) имя обработчика (длина 50).
//      * ИдентификаторФайла - УникальныйИдентификатор - идентификатор файла объекта.
//
Процедура ДобавитьЗапись(ДанныеЗаписи) Экспорт
    
    ОбязательныеСвойства = Новый Массив;
    ОбязательныеСвойства.Добавить("УчетнаяСистема");
    ОбязательныеСвойства.Добавить("ИдентификаторОбъекта");
    ОбязательныеСвойства.Добавить("Обработчик");
    
    СвойствоНеЗадано = ИнтеграцияОбъектовОбластейДанныхСловарь.СвойствоНеЗадано();
    Ошибки = Новый Массив;
    Для Каждого Свойство Из ОбязательныеСвойства Цикл
        Если Не ДанныеЗаписи.Свойство(Свойство) Тогда
            Ошибки.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СвойствоНеЗадано, Свойство)); 
        КонецЕсли;     	
    КонецЦикла;
    Если Ошибки.Количество() > 0 Тогда
        ВызватьИсключение СтрСоединить(Ошибки, Символы.ПС);    
    КонецЕсли; 
    
    МенеджерЗаписи = СоздатьМенеджерЗаписи();
    ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ДанныеЗаписи);
    МенеджерЗаписи.Записать();
    
КонецПроцедуры

// Удаляет запись объекта к отправке из регистра.
//
// Параметры:
//  Отбор - Структура - данные для удаления с полями:
//	 * УчетнаяСистема - см. ОпределяемыйТип.УчетныеСистемыИнтеграцииОбластейДанных - учетная система.
//	 * ИдентификаторОбъекта - Строка - идентификатор объекта (длина 50)
//	 * Обработчик - Строка - имя обработчика (длина 50)
//
Процедура УдалитьЗаписи(Отбор) Экспорт
    
    Набор = СоздатьНаборЗаписей();
    Набор.Отбор.УчетнаяСистема.Установить(Отбор.УчетнаяСистема, Истина);
    ДопОтборы = Новый Массив;
    ДопОтборы.Добавить("ИдентификаторОбъекта");
    ДопОтборы.Добавить("Обработчик");
    
    Для Каждого ДопОтбор Из ДопОтборы Цикл
        Если Отбор.Свойство(ДопОтбор) И ЗначениеЗаполнено(Отбор[ДопОтбор]) Тогда
            ОтборНабора = Набор.Отбор[ДопОтбор]; // ЭлементОтбора
            ОтборНабора.Установить(Отбор[ДопОтбор], Истина);
        КонецЕсли; 
    КонецЦикла; 
    
    Набор.Записать();
    
КонецПроцедуры

// Возвращает количество объектов к отправке по учетной системе.
//
// Параметры:
//  УчетнаяСистема - см. ОпределяемыйТип.УчетныеСистемыИнтеграцииОбластейДанных - учетная система. 
// 
// Возвращаемое значение:
//   - Число - количество объектов.
//
Функция Количество(УчетнаяСистема) Экспорт
	
    Запрос = Новый Запрос;
    Запрос.Текст = 
    	"ВЫБРАТЬ
        |   КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ОбъектыКОтправке.ИдентификаторОбъекта) КАК Количество
        |ИЗ
        |   РегистрСведений.ОбъектыКОтправке КАК ОбъектыКОтправке
        |       ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияИнтеграцииОбъектов КАК СостоянияИнтеграцииОбъектов
        |       ПО ОбъектыКОтправке.УчетнаяСистема = СостоянияИнтеграцииОбъектов.УчетнаяСистема
        |           И ОбъектыКОтправке.ИдентификаторОбъекта = СостоянияИнтеграцииОбъектов.ИдентификаторОбъекта
        |ГДЕ
        |   ОбъектыКОтправке.УчетнаяСистема = &УчетнаяСистема
        |   И ЕСТЬNULL(СостоянияИнтеграцииОбъектов.Состояние, &ПустоеСостояние) <> &Ошибка";
    
    Запрос.УстановитьПараметр("УчетнаяСистема", УчетнаяСистема);
    Запрос.УстановитьПараметр("Ошибка", Перечисления.СостоянияИнтеграцииОбъектов.Ошибка);
    Запрос.УстановитьПараметр("ПустоеСостояние", Перечисления.СостоянияИнтеграцииОбъектов.ПустаяСсылка());
    
    Результат = Запрос.Выполнить();
    Выборка = Результат.Выбрать();
    Если Выборка.Следующий() Тогда
        Возврат Выборка.Количество;
    Иначе 
        Возврат 0;
    КонецЕсли; 
	
КонецФункции
 
Функция НовыйДанныеЗаписи() Экспорт
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.ОбъектыКОтправке;
    ДанныеЗаписи = Новый Структура;
    Для Каждого Измерение Из МетаданныеРегистра.Измерения Цикл
    	ДанныеЗаписи.Вставить(Измерение.Имя, Измерение.Тип.ПривестиЗначение(Неопределено));
    КонецЦикла;
    Для Каждого Ресурс Из МетаданныеРегистра.Ресурсы Цикл
    	ДанныеЗаписи.Вставить(Ресурс.Имя, Ресурс.Тип.ПривестиЗначение(Неопределено));
    КонецЦикла;

    Возврат ДанныеЗаписи;
	
КонецФункции

#КонецОбласти

#КонецЕсли